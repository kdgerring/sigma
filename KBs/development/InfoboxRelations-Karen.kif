(instance versionNumber BinaryPredicate)
(documentation versionNumber EnglishLanguage "(&%versionNumber ?OBJ ?VERSION) means
that &%Artifact ?OBJ, which is a &%version of some other &%Artifact, is in a version
identified by &%SymbolicString ?VERSION")
(termFormat EnglishLanguage versionNumber "version number")

(domainSubclass versionNumber 1 Artifact)
(domain versionNumber 2 SymbolicString)

(=>
  (and
    (version ?OBJ1 ?OBJ2)
    (versionNumber ?OBJ1 ?VERSION1)
    (versionNumber ?OBJ2 ?VERSION2))
  (not (equal ?VERSION1 ?VERSION2)))
  
(=>
  (versionNumber ?OBJ ?VERSION)
  (exists (?OBJ2)
    (version ?OBJ ?OBJ2)))

(subclass ViscosityMeasure FunctionQuantity)
(documentation ViscosityMeasure EnglishLanguage "&%ViscosityMeasure refers to the 
measure of the resistance of a fluid which is being deformed by either shear stress 
or tensile stress.")
(termFormat EnglishLanguage ViscosityMeasure "viscosity")

(=>
  (and
    (measure ?OBJ ?MEAS)
    (instance ?MEAS ViscosityMeasure))
  (instance ?OBJ Fluid))
  
(=>
  (equal
    (PerFn (MeasureFn ?NUMBER Pascal) (MeasureFn 1 SecondDuration)) ?QUANT)
  (instance ?QUANT ViscosityMeasure))

(instance utcOffset BinaryPredicate)
(documentation utcOffset EnglishLanguage "(&%utcOffset ?TZ ?OFFSET ?REL) means that 
&%TimeZone ?TZ is &%TimeDuration ?OFFSET ahead or behind of &%CoordinatedUniversalTimeZone. 
?REL is the &%AssociativeFn used to determine if ?OFFSET is added or
subtracted from UTC time.")
(termFormat EnglishLanguage utcOffset "utc offset")

(domain utcOffset 1 TimeZone)
(domain utcOffset 2 TimeDuration)
(domain utcOffset 3 AssociativeFn)

(=>
  (utcOffset ?TZ ?OFFSET ?REL)
  (or
    (equal ?REL SubtractionFn)
    (equal ?REL AdditionFn)))
    
(=>
  (utcOffset ?TZ ?OFFSET ?REL)
  (exists (?UTC ?TIME)
    (and
      (equal ?UTC (RelativeTimeFn ?TIME ?TZ))
      (equal ?OFFSET (?REL ?UTC ?TIME)))))
  
(instance timezone BinaryPredicate)
(documentation timezone EnglishLanguage "(&%timezone ?AREA ?ZONE) relates an area
to a specific timezone.")
(termFormat EnglishLanguage timezone "timezone")

(domain timezone 1 GeographicArea)
(domain timezone 2 TimeZone)


(instance Watching IntentionalProcess)
(documentation Watching EnglishLanguage "&%Watching, in the sense of 'watching a show'
or 'watching TV' denotes that there is some element of &%Interpreting, as well as
&%Looking and &%Listening to dialogue.")
(termFormat EnglishLanguage Watching "watching")

(=>
  (instance ?W Watching)
  (exists (?LOOK ?LISTEN ?INTERPRET)
    (and
      (instance ?LOOK Looking)
      (instance ?LISTEN Listening)
      (instance ?INTERPRET Interpreting)
      (subProcess ?LOOK ?W)
      (subProcess ?LISTEN ?W)
      (subProcess ?INTERPRET ?W))))

(subAttribute VoiceActor Actor)
(documentation VoiceActor EnglishLanguage "Like an &%Actor, a &%VoiceActor is also an
agent of the dramatic performance.  However, only his voice can be heard and not the
actor himself")
(termFormat EnglishLanguage VoiceActor "voice actor")

(=>
  (and
    (attribute ?ACTOR VoiceActor)
    (instance ?DRAMA DramaticPerformance)
    (agent ?DRAMA ?ACTOR)
    (instance ?SPEAK LinguisticCommunication)
    (agent ?SPEAK ?ACTOR)
    (subProcess ?SPEAK ?DRAMA)
    (instance ?W Watching)
    (patient ?W ?DRAMA)
    (agent ?W ?VIEWER))
  (and
    (not
      (exists (?S)
        (and
          (instance ?S Seeing)
          (patient ?S ?ACTOR)
          (agent ?S ?VIEWER))))
    (exists (?H)
      (and
        (instance ?H Hearing)
        (patient ?H ?ACTOR)
        (agent ?H ?VIEWER)))))
        
(instance voltageRequirement BinaryPredicate)
(documentation voltageRequirement EnglishLanguage "(&%voltageRequirement ?OBJ ?MEAS)
means that &%Object ?OBJ needs &%Measure ?MEAS in order to function")
(termFormat EnglishLanguage voltageRequirement "voltage requirement")

(domainSubclass voltageRequirement 1 Object)
(domain voltageRequirement 2 Measure)

(=>
  (and
    (voltageRequirement ?CLASS ?MEAS)
    (instance ?OBJ ?CLASS)
    (instance ?PROC Process)
    (instrument ?PROC ?OBJ))
  (exists (?ELEC)
    (and
      (instance ?ELEC Electricity)
      (resource ?PROC ?ELEC)
      (measure ?ELEC ?MEAS))))
      
(instance yearManufactured BinaryPredicate)
(documentation yearManufactured EnglishLanguage "(&%yearsManufactured ?OBJ ?TIME)
means that &%Object ?OBJ was manufactured during the &%TimeInterval ?TIME.")
(termFormat EnglishLanguage yearManufactured "year manufactured")

(domainSubclass yearManufactured 1 Object)
(domainSubclass yearManufactured 2 Year)

(=>
  (yearManufactured ?CLASS ?YEAR)
  (exists (?OBJ ?PROC ?TIME)
    (and
      (instance ?OBJ ?CLASS)
      (instance ?PROC Manufacturing)
      (result ?PROC ?OBJ)
      (instance ?TIME ?YEAR)
      (temporallyBetweenOrEqual
        (BeginFn ?TIME)
        (EndFn (WhenFn ?PROC))
        (EndFn ?TIME)))))
        
(instance attributeAtTime TernaryPredicate)
(documentation attributeAtTime EnglishLanguage "(&%attributeAtTime ?ENT ?ATTR ?TIME) 
means that the &%Attribute ?ATTR applies to &%Entity ?ENT during &%TimeInterval ?TIME")
(termFormat EnglishLanguage attributeAtTime "attribute at time")

(domain attributeAtTime 1 Entity)
(domain attributeAtTime 2 Attribute)
(domain attributeAtTime 3 TimePoint)

(=>
  (attributeAtTime ?ENT ?ATTR ?TIME)
  (holdsDuring ?TIME
    (attribute ?ENT ?ATTR)))

