;; This is an initial check in of content in an early stage of
;; development and is not yet suitable for general use.

;; Access to and use of these products is governed by the GNU General
;; Public License <http://www.gnu.org/copyleft/gpl.html>.  By using
;; these products, you agree to be bound by the terms of the GPL.

;; Those who are interested in making use of this ontology are urged
;; to contact Adam Pease (adampease@earthlink.net) and Jan Scheffczyk
;; (jan.scheffczyk@gmx.net).  Version date: May, 2006 We ask the
;; people using or referencing this work cite our primary paper:

;; Niles, I., and Pease, A.  2001.  Towards a Standard Upper Ontology.
;; In Proceedings of the 2nd International Conference on Formal
;; Ontology in Information Systems (FOIS-2001), Chris Welty and Barry
;; Smith, eds, Ogunquit, Maine, October 17-19, 2001.  See also
;; http://www.ontologyportal.org

(subclass Altimeter MeasuringDevice)
(documentation Altimeter "A &%Device that measures the vertical
distance to the some reference point of the surface below. This may be
the mean sea level or the actual surface of the earth.")

;; altimeters measure AltitudeMeasures
(=>
  (and
    (instance ?MEASURE Measuring)
    (result ?MEASURE ?QUANTITY)
    (instrument ?MEASURE ?ALTIMETER)
    (instance ?ALTIMETER Altimeter))
  (instance ?QUANTITY AltitudeMeasure))


(subclass AbsoluteAltimeter Altimeter)
(documentation AbsoluteAltimeter "A type of &%Altimeter that measures
vertical distance to the surface below, using radio, radar, sonic,
laser, or capacitive technology.")

;; an AbsoluteAltimeter measures the distance of an object to the
;; surface of planet earth
(=>
  (and
    (instance ?MEASURE Measuring)
    (patient ?MEASURE ?OBJ)
    (result ?MEASURE ?QUANTITY)
    (instrument ?MEASURE ?ABSOLUTEMETER)
    (instance ?ABSOLUTEMETER AbsoluteAltimeter))
  (exists (OBJ2)
    (and
      (altitude ?OBJ ?OBJ2 ?QUANTITY)
      (surface ?OBJ2 PlanetEarth))))

(instance absoluteHeight SingleValuedRelation)
(instance absoluteHeight SpatialRelation)
(instance absoluteHeight BinaryPredicate)
(domain absoluteHeight 1 Aircraft)
(domain absoluteHeight 1 AltituteMeasure)

(documentation absoluteHeight "The height of an aircraft directly
above the surface or terrain over which it is flying.")

(<=>
  (and
    (altitude ?AIRCRAFT ?OBJ2 ?QUANTITY)
    (surface ?OBJ2 PlanetEarth)
    (instance ?AIRCRAFT Aircraft))
  (absoluteHeight ?AIRCRAFT ?QUANTITY))



(subclass BarometricAltimeter Altimeter)
(subclass BarometricAltimeter PressureMeasuringDevice)
(documentation AbsoluteAltimeter "A type of &%Altimeter which measures
vertical distance to the mean sea level, using pressure level and
calculated according to the standard atmosphere laws.")


;; a BarometricAltimeter measures the distance of some object to the
;; sea level
(=>
  (and
    (instance ?MEASURE Measuring)
    (patient ?MEASURE ?OBJ)
    (result ?MEASURE ?QUANTITY)
    (instrument ?MEASURE ?BAROMETER)
    (instance ?BAROMETER BarometricAltimeter))
  (altitude ?OBJ SeaLevel ?QUANTITY))

;; if the object used in the measurement is on planet earth then we
;; measure the elevation of the object
(=>
  (and
    (instance ?MEASURE Measuring)
    (patient ?MEASURE ?OBJ)
    (surface ?OBJ PlanetEarth)
    (result ?MEASURE ?QUANTITY)
    (instrument ?MEASURE ?BAROMETER)
    (instance ?BAROMETER BarometricAltimeter))
  (elevation ?OBJ ?QUANTITY))


(subclass AltitudeMeasure LengthMeasure)

(documentation AltitudeMeasure "The vertical distance of a level, a
point or an object considered as a point, measured from mean sea
level.")

(subclass Centimeter AltitudeMeasure)
(subclass Meter AltitudeMeasure)
(subclass FootLength AltitudeMeasure)
(subclass Mile AltitudeMeasure)
