
;; This is an initial check in of content in an early stage of
;; development and is not yet suitable for general use.

;; Access to and use of these products is governed by the GNU General
;; Public License <http://www.gnu.org/copyleft/gpl.html>.  By using
;; these products, you agree to be bound by the terms of the GPL.

;; Those who are interested in making use of this ontology are urged
;; to contact Adam Pease (adampease@earthlink.net) and Jan Scheffczyk
;; (jan.scheffczyk@gmx.net).  Version date: June, 2006 We ask the
;; people using or referencing this work cite our primary paper:

;; Niles, I., and Pease, A.  2001.  Towards a Standard Upper Ontology.
;; In Proceedings of the 2nd International Conference on Formal
;; Ontology in Information Systems (FOIS-2001), Chris Welty and Barry
;; Smith, eds, Ogunquit, Maine, October 17-19, 2001.  See also
;; http://www.ontologyportal.org

(instance DislocatedCivilian SocialRole)

(documentation DislocatedCivilian "A broad term that includes a
displaced person, an evacuee, an expellee, an internally displaced
person, a migrant, a refugee, or a stateless person. Also called DC.
See also displaced person; evacuee; expellee; internally displaced
person; migrant; refugee; stateless person.")

;; a dislocated civilian is no member of the military

(=>
  (holdsDuring ?NOW
    (attribute ?P DislocatedCivilian))
  (not
    (holdsDuring ?NOW
      (instance ?P MilitaryForce))))

;; a dislocated civilian is away from home and there was some
;; translocation process before that moved her to the new place

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P DislocatedCivilian))
    (home ?P ?HOME)
    (located ?P ?AREA))
  (and
    (not
      (partlyLocated ?HOME ?AREA))
    (not
      (equal ?HOME ?AREA))
    (exists (?TRANS)
      (and
        (instance ?TRANS Translocation)
        (agent ?TRANS ?P)
        (origin ?TRANS ?HOME)
        (destination ?TRANS ?AREA)
        (before
          (EndFn (WhenFn ?TRANS))
          ?NOW)))))



(instance DisplacedPerson SocialRole)
(subAttribute DisplacedPerson DislocatedCivilian)

(documentation DisplacedPerson "A civilian who is involuntarily
outside the national boundaries of his or her country. See also
evacuee; refugee.")

;; a displaced person is located in a different area then her normal
;; residency and she doe not like to be there

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P DisplacedPerson))
    (member ?P
      (ResidentFn ?HOME))
    (located ?P ?AREA)
    (instance ?AREA Nation))
  (and
    (not
      (equal ?HOME ?AREA))
    (holdsDuring ?NOW
      (disapproves ?P
        (located ?P ?AREA)))))

(instance InternallyDisplacedPerson SocialRole)
(subAttribute InternallyDisplacedPerson DislocatedCivilian)

(documentation InternallyDisplacedPerson "Any person who has left
their residence by reason of real or imagined danger but has not left
the territory of their own country.")

;; an IDP left her home, did this involuntarily but is still located
;; within the same nation

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P InternallyDisplacedPerson))
    (home ?P ?HOME)
    (located ?P ?AREA))
  (exists (?NATION)
    (and
      (located ?HOME ?NATION)
      (located ?AREA ?NATION)
      (instance ?NATION Nation)
      (holdsDuring ?NOW
        (disapproves ?P
          (located ?P ?AREA))))))

;; for an IDP there must have existed some process that she considered
;; dangerous

(=>
  (holdsDuring ?NOW
    (attribute ?P InternallyDisplacedPerson))
  (exists (?PROCESS ?BEFORE)
    (and
      (instance ?PROCESS Process)
      (equal ?BEFORE
        (WhenFn ?PROCESS))
      (before 
        (EndFn ?BEFORE)
        ?NOW)
      (holdsDuring ?BEFORE
        (believes ?P
          (attribute ?PROCESS Dangerous))))))

(instance Evacuee SocialRole)
(subAttribute Evacuee DislocatedCivilian)

(documentation Evacuee "A civilian removed from a place of residence
by military direction for reasons of personal security or the
requirements of the military situation. See also displaced person;
expellee; refugee.")

;; the reason of the translocation of an evacuee is a military operation

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P Evacuee))
    (home ?P ?HOME)
    (located ?P ?AREA)
    (instance ?TRANS Translocation)
    (agent ?TRANS ?P)
    (origin ?TRANS ?HOME)
    (destination ?TRANS ?AREA))
  (exists (?PROCESS)
    (and
      (instance ?PROCESS MilitaryOperation)
      (patient ?PROCESS ?P)
      (causes ?PROCESS ?TRANS)
      (before 
        (EndFn (WhenFn ?PROCESS))
        ?NOW))))


(instance Expellee  SocialRole)
(subAttribute Expellee DislocatedCivilian)

(documentation Expellee "A civilian outside the boundaries of the
country of his or her nationality or ethnic origin who is being
forcibly repatriated to that country or to a third country for
political or other purposes. See also displaced person; evacuee;
refugee.")

;; an expellee is outside the boundaries of his country and there is
;; some translocation bringing her back but she disapproves of that

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P Expellee))
    (home ?P ?HOME)
    (located ?P ?AREA))
  (exists (?NATION)
    (and
      (located ?HOME ?NATION)
      (not
        (partlyLocated ?AREA ?NATION))
      (instance ?NATION Nation)
      (exists (?TRANS)
        (and
          (patient ?TRANS ?P)
          (origin ?TRANS ?AREA)
          (destination ?TRANS ?HOME)
          (disapproves ?P
            (patient ?P ?TRANS)))))))


(instance Migrant SocialRole)
(subAttribute Migrant DislocatedCivilian)

(documentation Migrant "A person who (1) belongs to a normally
migratory culture who may cross national boundaries, or (2) has fled
his or her native country for economic reasons rather than fear of
political or ethnic persecution.")

;; a migrant did not leave his home because she feared something

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P Migrant))
    (home ?P ?HOME)
    (located ?P ?AREA)
    (instance ?TRANS Translocation)
    (agent ?TRANS ?P)
    (origin ?TRANS ?HOME)
    (destination ?TRANS ?AREA))
  (not
    (exists (?PROCESS ?REL)
      (and
        (causes ?PROCESS ?TRANS)
        (instance ?REL CaseRole)
        (fears ?P
          (holds ?REL ?ROCESS ?P))))))

(instance Refugee SocialRole)
(subAttribute Refugee DislocatedCivilian)

(documentation Refugee "A person who, by reason of real or imagined
danger, has left their home country or country of their nationality
and is unwilling or unable to return. See also dislocated civilian;
displaced person; evacuee; expellee; stateless person.")

;; for a refugee there must have existed some process that she considered
;; dangerous

(=>
  (holdsDuring ?NOW
    (attribute ?P Refugee))
  (exists (?PROCESS ?BEFORE)
    (and
      (instance ?PROCESS Process)
      (equal ?BEFORE
        (WhenFn ?PROCESS))
      (before 
        (EndFn ?BEFORE)
        ?NOW)
      (holdsDuring ?BEFORE
        (believes ?P
          (attribute ?PROCESS Dangerous))))))

;; a refugee is located in a different area then her normal
;; residency and she does not want to go back

(=>
  (and
    (holdsDuring ?NOW
      (attribute ?P DisplacedPerson))
    (member ?P
      (ResidentFn ?HOME))
    (located ?P ?AREA)
    (instance ?AREA Nation))
  (and
    (not
      (equal ?HOME ?AREA))
    (holdsDuring ?NOW
      (disapproves ?P
        (exists (?TRANS)
          (and
            (instance ?TRANS Translocation)
            (agent ?TRANS ?P)
            (origin ?TRANS ?AREA)
            (destination ?TRANS ?HOME)))))))


(instance StatelessPerson SocialRole)
(subAttribute StatelessPerson DislocatedCivilian)

(documentation StatelessPerson "Civilian who has been denationalized
or whose country of origin cannot be determined or who cannot
establish a right to the nationality claimed. See also dislocated
civilian; displaced person; evacuee; expellee; refugee.")

;; stateless persons do not have a country of residence

(=>
  (holdsDuring ?NOW
    (attribute ?P StatelessPerson))
  (not
    (exists (?NATION)
      (and
        (instance ?NATION Nation)
        (member ?P
          (ResidentFn ?NATION))))))

(instance Dangerous SubjectiveAssessmentAttribute)

(documentation Dangerous "from WordNet: causing fear or anxiety by
threatening great harm; a dangerous operation; a grave situation; a
grave illness; grievous bodily harm; a serious wound; a serious turn
of events; a severe case of pneumonia; a life-threatening disease.")

;; threatening is believed to be dangerous

(=>
  (and
    (instance ?THREATEN Threatening)
    (patient ?THREATEN ?PROP)
    (destination ?THREATEN ?DEST))
  (believes ?DEST
    (attribute ?PROP Dangerous)))

;; damaging is believed to be dangerous by cognitive agents

(=>
  (and
    (instance ?DAMAGE Damaging)
    (patient ?DAMAGE ?PROP)
    (destination ?DAMAGE ?DEST)
    (instance ?DEST CognitiveAgent))
  (believes ?DEST
    (attribute ?PROP Dangerous)))

