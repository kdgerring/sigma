;; ================================================
;;      Engineering ontology
;; ================================================
;;
;; (c) 2002 Michal Sevcenko <sevcenko@vc.cvut.cz>
;;
;; This is the source file for the engineering ontology, developed
;; within the KSMSA project (see http://virtual.cvut.cz/ksmsa/).
;; The ontology is built on top of SUMO ontology
;; (see http://www.ontologyportal.org).
;;
;; The KSMSA project is partly supported by the Czech Department of
;; Education grant no. FRV 2162/2002
;;
;; Version 1.0, 16 December 2002
;; Version 1.1  31 October 2005

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  MODULE Engineering-ontology  ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;
;;  TECHNICAL TERMS   ;;
;;;;;;;;;;;;;;;;;;;;;;;;

(documentation termFormat "Associates a concept with a natural language text.")
(instance termFormat Relation)
(domain termFormat 1 SetOrClass)
(domain termFormat 2 SymbolicString) 

(documentation format "Associates a relation or a function with a natural
language text.")
(instance format Relation)
(domain format 1 SetOrClass)
(domain format 2 SymbolicString)

(documentation AttrFn "For given class and for given attribute, return
a subclass of the class of object having the attribute")
(instance AttrFn BinaryFunction)
(range AttrFn Class)
(domain AttrFn 1 Class) 
(domain AttrFn 2 Attribute)

;;;;;;;;;;;;;;;;;
;;  LEXICON    ;;
;;;;;;;;;;;;;;;;;

(documentation lexicon "associates a SUMO concept with a lexicon word")
(instance lexicon QuaternaryRelation)
(domain lexicon 1 SetOrClass)
(domain lexicon 2 LexiconCategory)
(domain lexicon 3 SymbolicString)

(documentation LexiconCategory "WordNet category: noun, verb, adjective or adverb")
(subclass LexiconCategory InternalAttribute)

(documentation LexNoun "noun")
(instance LexNoun LexiconCategory)
(documentation LexVerb "verb")
(instance LexVerb LexiconCategory)
(documentation LexAdjective "adjective")
(instance LexAdjective LexiconCategory)
(documentation LexAdverb "adverb")
(instance LexAdverb LexiconCategory)

;;;;;;;;;;;;;;;;;;;;;;;;
;;  COMMON NOTIONS    ;;
;;;;;;;;;;;;;;;;;;;;;;;;

(documentation models "A relation signaling that certain model is
convenient for modeling of certain class of devices.")
(subclass models AsymmetricRelation)
(domain models 1 Model)
(domainSubclass models 2 EngineeringComponent)
(format models "%1 can be used as a model for %2")

(documentation Model "An abstract object that models certain aspect of a
physical object; is subject to abstraction and idealization.")
(subclass Model Abstract)

(documentation meronym "A relation similar to WordNet meronymy relation.
If class A is a meronym of class B, it means that instances of A
typically are parts of instances of B.")
(instance meronym AsymmetricRelation)
(instance meronym TransitiveRelation)
(domainSubclass meronym 1 Object)
(domainSubclass meronym 2 Object)
(format meronym "%1 is %n a meronym of %2")


(documentation PhysicalDimension "A physical dimension such as
length, mass, force etc.")
(lexicon PhysicalDimension LexNoun "physical dimension")
(subclass PhysicalDimension Quantity)

(documentation Length "&%PhysicalDimension of length, [m].")
(instance Length PhysicalDimension)

(documentation Velocity "&%PhysicalDimension of velocity, [m/s].")
(instance Velocity PhysicalDimension)

(documentation Force "&%PhysicalDimension of force, [N].")
(instance Force PhysicalDimension)

(documentation AngularVelocity "&%PhysicalDimension of angular velocity, [s^-1].")
(instance AngularVelocity PhysicalDimension)

(documentation Torque "&%PhysicalDimension of torque, [N/m].")
(instance Torque PhysicalDimension)

(documentation Voltage "&%PhysicalDimension of voltage, [V].")
(instance Voltage PhysicalDimension)

(documentation Current "&%PhysicalDimension of electrical current, [A].")
(instance Current PhysicalDimension)

(documentation Pressure "&%PhysicalDimension of pressure, [Pa],[N.m^-2].")
(instance Pressure PhysicalDimension)

(documentation VolumeFlow "&%PhysicalDimension of volume flow, [m^-3].")
(instance VolumeFlow PhysicalDimension)

(documentation Power "&%PhysicalDimension of power, [W].")
(instance Power PhysicalDimension)

(documentation Dimensionless "Dimensionless &%PhysicalDimension.")
(lexicon Dimensionless LexAdjective "dimensionless {physical dimension}")
(instance Dimensionless PhysicalDimension)

(documentation Modeling "A creative process of creating a model.")
(lexicon Modeling LexNoun "modeling")
(lexicon Modeling LexNoun "modelling")
(lexicon Modeling LexVerb "model")
(subclass Modeling IntentionalProcess)
(=>
	(instance ?MODELING Modeling)
	(exists (?MODEL)
		(and (instance ?MODEL Model)
			(result ?MODELING ?MODEL))))

(documentation abstractPart "A meronymy relation similar to &%part, but
for abstract rather than physical things.")
(termFormat abstractPart part)
(instance abstractPart PartialOrderingRelation)
(domain abstractPart 1 Abstract)
(domain abstractPart 2 Abstract)
(format abstractPart "%1 is %n a &%part of %2")

;;;;;;;;;;;;;;;;;;;;;;;
;;  MATH             ;;
;;;;;;;;;;;;;;;;;;;;;;;

(lexicon AbsoluteValueFn LexNoun "abs")
(lexicon AbsoluteValueFn LexNoun "absolute value")

(lexicon SineFn LexNoun "sinus")
(lexicon SineFn LexNoun "sin")

(lexicon CosineFn LexNoun "cosinus")
(lexicon CosineFn LexNoun "cos")

;;;;;;;;;;;;;;;;;;;;;;;
;;  EQUATIONS        ;;
;;;;;;;;;;;;;;;;;;;;;;;

(documentation Equation "a mathematical statement that two expressions are
equal.")
(lexicon Equation LexNoun "equation")
(subclass Equation Proposition)

(documentation DifferentialEquation "An &%Equation containing differentials
of a function ")
(subclass DifferentialEquation Equation)
(<=>
	(instance ?X DifferentialEquation)
	(instance ?X (AttrFn Equation DifferentialAttribute)))

(documentation LinearEquation "A polynomial &%Equation of the first degree.")
(subclass LinearEquation Equation)

(documentation NonlinearEquation "An &%Equation that is not
a &%LinearEquation.")
(subclass NonlinearEquation Equation)

(documentation AlgebraicEquation "An &%Equation that is not
a &%DifferentialEquation")
(subclass AlgebraicEquation Equation)

(documentation FirstOrderDifferentialEquation "A &%DifferentialEquation
where variables are differentiated only once.")
(lexicon FirstOrderDifferentialEquation LexNoun "first-order differential equation")
(subclass FirstOrderDifferentialEquation DifferentialEquation)

(documentation HigherOrderDifferentialEquation "A &%DifferentialEquation
where variables are differentiated more than once.")
(lexicon HigherOrderDifferentialEquation LexNoun "higher-order differential equation")
(subclass HigherOrderDifferentialEquation DifferentialEquation)

(disjointDecomposition DifferentialEquation FirstOrderDifferentialEquation HigherOrderDifferentialEquation)

(documentation PartialDifferentialEquation "A &%DifferentialEquation
involving a functions of more than one variable.")
(subclass PartialDifferentialEquation DifferentialEquation)

(documentation OrdinaryDifferentialEquation "A &%DifferentialEquation
that is not a &%PartialDifferentialEquation.")
(subclass OrdinaryDifferentialEquation DifferentialEquation)

(disjointDecomposition DifferentialEquation OrdinaryDifferentialEquation PartialDifferentialEquation)

(documentation BesselsEquation "Bessel's equation")
(lexicon BesselsEquation LexNoun "Bessel's equation")
(subclass BesselsEquation OrdinaryDifferentialEquation)
(subclass BesselsEquation NonlinearEquation)

(documentation VanderpolsEquation "Bessel's equation")
(lexicon VanderpolsEquation LexNoun "{Van der Pol's} equation")
(subclass VanderpolsEquation OrdinaryDifferentialEquation)
(subclass VanderpolsEquation NonlinearEquation)

(documentation SetOfEquations "A &%Set of equations")
(lexicon SetOfEquations LexNoun "set of equations")
(lexicon SetOfEquations LexNoun "equations")
(subclass SetOfEquations Set)

(documentation EquationAttribute "an attribute that applies to an equation
or to a set of equations")
(subclass EquationAttribute InternalAttribute)
(documentation "&%EquationAttribute applies to equations or sets of equations")
(=>
	(and
		(property ?OBJECT ?ATTRIBUTE)
		(instance ?ATTRIBUTE EquationAttribute))
	(or
		(instance ?OBJECT Equation)
		(instance ?OBJECT SetOfEquations)))

(documentation DifferentialAttribute "differential equation or set of equations")
(lexicon DifferentialAttribute LexAdjective "differential")
(instance DifferentialAttribute EquationAttribute)

(documentation AlgebraicAttribute "algebraic equation or set of equations")
(lexicon AlgebraicAttribute LexAdjective "algebraic")
(instance AlgebraicAttribute EquationAttribute)

(documentation AlgebroDifferentialAttribute "Set of both differential and algebraic equations")
(lexicon AlgebroDifferentialAttribute LexAdjective "algebro-differential")
(instance AlgebroDifferentialAttribute InternalAttribute)
(=>
	(and
		(property ?OBJECT ?ATTRIBUTE)
		(instance ?ATTRIBUTE AlgebroDifferentialAttribute))
	(instance ?OBJECT SetOfEquations))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  MATHEMATICAL MODELS      ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation MathematicalModel "A model that uses the mathematical
aparatus")
(subclass MathematicalModel Model)
(documentation MathematicalPendulum "A model of a &%Pendulum consisting
of a mass hanged on a stiff string.")
(subclass MathematicalPendulum MathematicalModel)

;;;;;;;;;;;;;;;;;;;;;;;;
;;  MULTIPOLES        ;;
;;;;;;;;;;;;;;;;;;;;;;;;

(documentation PhysicalDomain "An attribute of a multipole pole describing
the kind of physical interaction the pole models.")
(lexicon PhysicalDomain LexNoun "{physical domain}")
(subclass PhysicalDomain InternalAttribute)

(documentation Electrical "Electrical energetic interaction")
(lexicon Electrical LexNoun "electrical {physical domain}")
(instance Electrical PhysicalDomain)

(documentation Translatory "Mechanic translatory energetic interaction")
(lexicon Translatory LexNoun "translatory {physical domain}")
(instance Translatory PhysicalDomain)

(documentation Rotary "Mechanic rotary energetic interaction")
(lexicon Rotary LexNoun "rotary {physical domain}")
(instance Rotary PhysicalDomain)

(documentation FluidPower "Fluid power energetic interaction")
(lexicon FluidPower LexNoun "{fluid power} {physical domain}")
(instance FluidPower PhysicalDomain)

(documentation physicalDomain "Relation that holds for pairs of physical
dimensions that multiply up into a physical dimension of Power.
The first is considered for across variable, the second for through
variable.")
(instance physicalDomain TernaryRelation)
(format physicalDomain "%1 and %2 form physical domain %3")
(domain physicalDomain 1 PhysicalDimension)
(domain physicalDomain 2 PhysicalDimension)
(domain physicalDomain 3 PhysicalDomain)

(physicalDomain Velocity Force Translatory)
(physicalDomain AngularVelocity Torque Rotary)
(physicalDomain Voltage Current Electrical)
(physicalDomain Pressure VolumeFlow FluidPower)

(documentation MultipoleVariable "a variable that describes energetical
interactions between multipoles.")
(subclass MultipoleVariable Number)

(documentation MultipoleQuantity "a multipole variable that have physical
dimension and meaning.")
(subclass MultipoleQuantity Quantity)
(documentation hasDimension "multipole quantity has certain dimension")
(instance hasDimension SingleValuedRelation)
(format hasDimension "%1 has %n dimension %2")
(domain hasDimension 1 MultipoleQuantity)
(domain hasDimension 2 PhysicalDimension)
(documentation hasVariable "multipole quantity has certain variable")
(instance hasVariable SingleValuedRelation)
(format hasVariable "%1 has %n variable %2")
(domain hasVariable 1 MultipoleQuantity)
(domain hasVariable 2 MultipoleVariable)

(documentation Multipole "Basic element of a multipole diagram; a
multipole is a model of a component of a dynamic system; it can model a
real separable component, such as a motor of a vehicle, or just an
attribute of the system, such as inertia or friction; multipole interacts
with other multipoles through its poles.")
(lexicon Multipole LexNoun "multipole")
(subclass Multipole Model)

(documentation MultipoleAttribute "a set of tags that can be associated
with multipoles")
(subclass MultipoleAttribute InternalAttribute)

(documentation IndependentMultipole "Asserts that a constitutive relation
of a multipole does not refer to other variables than terminal or
inner.")
(instance IndependentMultipole MultipoleAttribute)

;;;;;;;;

(documentation DynamicMultipole "Asserts that the constitutive relation of
a multipole does depend on time.")
(instance DynamicMultipole MultipoleAttribute)

;;;;;;;;

(documentation LinearMultipole "Asserts that the constitutive relation of
a multipole is linear.")
(instance LinearMultipole MultipoleAttribute)

(documentation MultipoleModel "Model of a physical system consisting of
mutually interconnected multipoles.")
(lexicon MultipoleModel LexNoun "multipole model")
(subclass MultipoleModel Model)

(documentation ElectricalMultipoleModel "A &%MultipoleModel containing only
electrical multipoles.")
(lexicon ElectricalMultipoleModel LexNoun "electrical circuit")
(subclass ElectricalMultipoleModel MultipoleModel)

(documentation NonlinearCircuit "An &%ElectricalMultipoleModel containing
nonlinear multipoles.")
(subclass NonlinearCircuit ElectricalMultipoleModel)

(documentation RLCircuit "An &%ElectricalMultipoleModel containing
a &%ResistorElement and an &%InductorElement.")
(lexicon RLCircuit LexNoun "RL circuit")
(subclass RLCircuit ElectricalMultipoleModel)

(documentation RLCCircuit "An &%ElectricalMultipoleModel containing
a &%ResistorElement, an &%InductorElement and a &%CapacitorElement.")
(lexicon RLCCircuit LexNoun "RLC circuit")
(subclass RLCCircuit ElectricalMultipoleModel)

(documentation RCCircuit "An &%ElectricalMultipoleModel containing
a &%ResistorElement and a &%CapacitorElement.")
(lexicon RCCircuit LexNoun "RC circuit")
(subclass RCCircuit ElectricalMultipoleModel)

(documentation MultipoleModeling "modeling of a dynamic system by means of
its representation by a multipole diagram.")
(lexicon MultipoleModeling LexNoun "multipole modeling")
(lexicon MultipoleModeling LexNoun "multipole modelling")
(subclass MultipoleModeling Modeling)
(=>
	(instance ?MODELING MultipoleModeling)
	(exists (?MODEL)
		(and (instance ?MODEL MultipoleModel)
			(result ?MODELING ?MODEL))))

(documentation MultipoleDiagram "Graphical representation of a
&%MultipoleModel.")
(lexicon MultipoleDiagram LexNoun "multipole diagram")
(subclass MultipoleDiagram ContentBearingObject)

(documentation MultipolePole "A part of multipole pole that models
a single energetical interaction.")
(lexicon MultipolePole LexNoun "multipole pole")
(lexicon MultipolePole LexNoun "pole")
(lexicon MultipolePole LexNoun "{pole of} multipole")
(subclass MultipolePole Model)

(documentation "If pole belongs to a multipole, it also belongs to
one of its sections.")
(=>
	(and
		(instance ?POLE MultipolePole)
		(abstractPart ?POLE ?MULTIPOLE))
	(exists (?SECTION)
		(and
			(instance ?SECTION MultipoleSection)
			(abstractPart ?POLE ?SECTION)
			(abstractPart ?SECTION ?MULTIPOLE))))

(documentation hasAcrossVariable "multipole pole has across variable")
(instance hasAcrossVariable AsymmetricRelation)
(format hasAcrossVariable "%1 has %n %2 as an across variable")
(domain hasAcrossVariable 1 MultipolePole)
(domain hasAcrossVariable 2 MultipoleVariable)
(documentation "Each pole has an across variable")

(documentation hasThroughVariable "multipole pole has through variable")
(instance hasThroughVariable AsymmetricRelation)
(format hasThroughVariable "%1 has %n %2 as a through variable")
(domain hasThroughVariable 1 MultipolePole)
(domain hasThroughVariable 2 MultipoleVariable)
(documentation "Each pole has an through variable")

(documentation "variables of a pole must be related power variables")
(=>
	(and
		(hasAcrossVariable ?POLE ?ACROSS)
		(hasThroughVariable ?POLE ?THROUGH)
		(hasVariable ?QACROSS ?ACROSS)
		(hasVariable ?QTHROUGH ?THROUGH)
		(hasDimension ?QACROSS ?DACROSS)
		(hasDimension ?QTHROUGH ?DTHROUGH))
	(exists (?DOMAIN)
		(physicalDomain ?DACROSS ?DTHROUGH ?DOMAIN)))

(documentation MultipoleSection "A set of poles that is subject to
the postulate of continuity. A multipole may consist of one or
more sections. Sections of a single multipole do not overlap
and cover all its poles.")
(lexicon MultipoleSection LexNoun "multipole section")
(lexicon MultipoleSection LexNoun "section")
(lexicon MultipoleSection LexNoun "{section of} multipole")
(subclass MultipoleSection Model)

(documentation "Each multipole must have at least one section.")
(=>
	(instance ?MULTIPOLE Multipole)
	(exists (?SECTION)
		(and
			(instance ?SECTION MultipoleSection)
			(abstractPart ?SECTION ?MULTIPOLE))))

(documentation "Each section must have at least two distinct poles.")
(=>
	(instance ?SECTION MultipoleSection)
	(exists (?POLE1 ?POLE2)
		(and
			(instance ?POLE1 MultipolePole)
			(instance ?POLE2 MultipolePole)
			(abstractPart ?POLE1 ?SECTION)
			(abstractPart ?POLE2 ?SECTION)
			(not (equal ?POLE1 ?POLE2)))))

(documentation MultipolePort "&%MultipoleSection that contains exactly two
poles.")
(subclass MultipolePort MultipoleSection)
(lexicon MultipolePort LexNoun "multipole port")
(lexicon MultipolePort LexNoun "port")
(lexicon MultipolePort LexNoun "{port of} multipole")
(documentation "Ports do not have three distinct poles.")
(=>
	(instance ?PORT MultipolePort)
	(not (exists (?POLE1 ?POLE2 ?POLE3)
		(and
			(instance ?POLE1 MultipolePole)
			(instance ?POLE2 MultipolePole)
			(instance ?POLE3 MultipolePole)
			(abstractPart ?POLE1 ?PORT)
			(abstractPart ?POLE2 ?PORT)
			(abstractPart ?POLE3 ?PORT)
			(not (equal ?POLE1 ?POLE2))
			(not (equal ?POLE2 ?POLE3))
			(not (equal ?POLE1 ?POLE3))))))

(documentation Twopole "A &%Multipole with exactly two poles.")
(lexicon Twopole LexNoun "twopole")
(lexicon Twopole LexNoun "oneport")
(subclass Twopole Multipole)
(documentation "Twopole has exactly one port.")
(=>
	(instance ?TWOPOLE Twopole)
	(exists (?PORT)
		(and
			(instance ?PORT MultipolePort)
			(abstractPart ?PORT ?TWOPOLE)
			(not (exists (?SECTION)
				(and
					(instance ?SECTION MultipoleSection)
					(abstractPart ?SECTION ?TWOPOLE)
					(not (equal ?SECTION ?PORT))))))))

(documentation PureTwopole "A &%Twopole that models single physical
phenomenon, its constitutive relation has special structure.")
(lexicon PureTwopole LexNoun "pure twopole")
(lexicon PureTwopole LexNoun "physical element")
(subclass PureTwopole Twopole)

(documentation IdealSwitch "A &%PureTwopole that models an ideal switch.")
(subclass IdealSwitch PureTwopole)

(documentation IdealTwopole "A &%PureTwopole that is linear.")
(lexicon IdealTwopole LexNoun "ideal twopole")
(lexicon IdealTwopole LexNoun "ideal {physical element}")
(subclass IdealTwopole PureTwopole)

(documentation Dissipator "A &%PureTwopole that models a dissipation of energy.")
(subclass Dissipator PureTwopole)

(documentation AcrossVariableAccumulator "A &%PureTwopole that accumulates across variable.")
(lexicon AcrossVariableAccumulator LexNoun "{across variable} accumulator")
(subclass AcrossVariableAccumulator PureTwopole)

(documentation ThroughVariableAccumulator "A &%PureTwopole that accumulates through variable.")
(lexicon ThroughVariableAccumulator LexNoun "{through variable} accumulator")
(subclass ThroughVariableAccumulator PureTwopole)

(documentation Source "A &%PureTwopole that models a generator of either across or through variable.")
(subclass Source PureTwopole)

(documentation AcrossVariableSource "A &%Source that models a generator of across variable.")
(lexicon AcrossVariableSource LexNoun "{across variable} source")
(subclass AcrossVariableSource Source)

(documentation ThroughVariableSource "A &%Source that models a generator of through variable.")
(lexicon ThroughVariableSource LexNoun "{through variable} source")
(subclass ThroughVariableSource Source)

(documentation ElectricalTwopole "A &%PureTwopole from electrical energy domain.")
(subclass ElectricalTwopole PureTwopole)

(documentation ResistorElement "A &%Dissipator from electrical energy domain.")
(lexicon ResistorElement LexNoun "resistor")
(subclass ResistorElement Dissipator)
(subclass ResistorElement ElectricalTwopole)

(documentation CapacitorElement "An &%AcrossVariableAccumulator from electrical energy domain.")
(lexicon CapacitorElement LexNoun "capacitor")
(subclass CapacitorElement AcrossVariableAccumulator)
(subclass CapacitorElement ElectricalTwopole)

(documentation InductorElement "A &%ThroughVariableAccumulator from electrical energy domain.")
(lexicon InductorElement LexNoun "inductor")
(subclass InductorElement ThroughVariableAccumulator)
(subclass InductorElement ElectricalTwopole)

(documentation TranslatoryTwopole "A &%PureTwopole from translatory energy domain.")
(subclass TranslatoryTwopole PureTwopole)

(documentation DamperElement "A &%Dissipator from translatory energy domain.")
(lexicon DamperElement LexNoun "damper")
(subclass DamperElement Dissipator)
(subclass DamperElement TranslatoryTwopole)

(documentation Inertor "An &%AcrossVariableAccumulator from translatory energy domain.")
(lexicon Inertor LexNoun "inertor")
(subclass Inertor AcrossVariableAccumulator)
(subclass Inertor TranslatoryTwopole)

(documentation SpringElement "A &%ThroughVariableAccumulator from translatory energy domain.")
(lexicon SpringElement LexNoun "spring")
(subclass SpringElement ThroughVariableAccumulator)
(subclass SpringElement TranslatoryTwopole)

(documentation Fourpole "A &%Multipole with exactly four poles.")
(subclass Fourpole Multipole)

(documentation Twoport "A &%Multipole with exactly two sections.")
(subclass Twoport Fourpole)

(documentation IdealOperationalAmplifier "A &%Fourpole modeling an
&%OperationalAmplifier with ideal properties.")
(lexicon IdealOperationalAmplifier LexNoun "ideal operational amplifier")
(lexicon IdealOperationalAmplifier LexNoun "operational amplifier")
(subclass IdealOperationalAmplifier Fourpole)

(documentation Transducer "A &%Device which is capable of converting one 
form of energy into another.  Formally, a &%Twoport that neither stores 
nor dissipates, but only transfers energy between its two ports.") 
(lexicon Transducer LexNoun "transducer")
(subclass Transducer Twoport)

(documentation Transformer "A &%Transducer for which the ratio of
across variables and through variables respectively is equal.")
(lexicon Transformer LexNoun "transformer")
(subclass Transformer Transducer)

(documentation Gyrator "A &%Transducer for which the ratio of
across variable on one side and through variable on the
other side is equal to the ratio of the remaining two variables.")
(lexicon Gyrator LexNoun "gyrator")
(subclass Gyrator Transducer)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;           NATURAL-PROCESSES                 ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation NaturalProcess "A &%Process that take place in nature
spontanously.")
(subclass NaturalProcess Process)
(disjoint NaturalProcess IntentionalProcess)

(documentation MechanicalProcess "A &%Process in which mechanical
interactions take place")
(subclass MechanicalProcess NaturalProcess)

(documentation ElectricalProcess "A &%Process in which electrical
interactions take place")
(subclass ElectricalProcess NaturalProcess)

(documentation Friction "A &%MechanicalProcess in which mechanical
energy is converted into a heat")
(subclass Friction MechanicalProcess)

(documentation Resonance "a vibration of large amplitude produced by
a relatively small vibration near the same frequency of vibration
as the natural frequency of the resonating system.")
(subclass Resonance NaturalProcess)

(documentation MechanicalResonance "a resonance of mechanical energy.")
(subclass MechanicalResonance Resonance)
(subclass MechanicalResonance MechanicalProcess)

(documentation ElectricalResonance "a resonance of electrical energy.")
(subclass ElectricalResonance Resonance)
(subclass ElectricalResonance ElectricalProcess)

(documentation SeriesResonance "Resonance taking place in a parallel RLC
circuit, i.e. in a circuit where its elements are connected in-parallel.")
(subclass SeriesResonance ElectricalResonance)

(documentation ParallelResonance "Resonance taking place in a series RLC
circuit, i.e. in a circuit where its elements are connected in-series.")
(subclass ParallelResonance ElectricalResonance)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;        PHYSICAL-ATTRIBUTES                   ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation PhysicalAttribute "An &%InternalAttribute given by physical
properties of the object.")
(subclass PhysicalAttribute InternalAttribute)

(documentation Conductivity "the ability of a material to lead current")
(instance Conductivity PhysicalAttribute)
(contraryAttribute Conductivity Resistivity)

(documentation Resistivity "A material's opposition to the flow of
electric current.")
(lexicon Resistivity LexNoun "resistivity")
(instance Resistivity PhysicalAttribute)

(documentation Inductance "A property of an electric circuit by which an
electromotive force is induced in it by a variation of current.")
(instance Inductance PhysicalAttribute)

(documentation MutualInductance "Property whereby an electromotive force is
induced in a circuit by variation of current in a neighboring circuit.") 
(subclass MutualInductance Inductance)

(documentation Stiffness "The physical property of being inflexible
and hard to stretch.")
(instance Stiffness PhysicalAttribute)

(documentation Compliance "The ability of material to stretch or bend.")
(instance Compliance PhysicalAttribute)
(contraryAttribute Stiffness Compliance)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;        ABSTRACT-ATTRIBUTES                   ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation Nonlinear "The description of the entity (e.g. a &%Model)
involves nonlinear functions.")
(subclass Nonlinear InternalAttribute)
(lexicon Nonlinear LexNoun "nonlinearity")
(lexicon Nonlinear LexAdjective "nonlinear")

(documentation Backlashless "With no backlash.")
(lexicon Backlashless LexAdjective "backlashless")
(subclass Backlashless InternalAttribute)

(documentation Brushless "With no brushes (of electrical motors)")
(lexicon Brushless LexAdjective "brushless")
(subclass Brushless InternalAttribute)
(=>
	(and
		(attribute ?OBJECT ?ATTRIBUTE)
		(instance ?ATTRIBUTE Brushless))
	(instance ?OBJECT ElectricalMotor))

(documentation Autonomous "autonomous")
(lexicon Autonomous LexAdjective "autonomous")
(lexicon Autonomous LexAdverb "autonomously")
(subclass Autonomous InternalAttribute)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                DEVICES                   ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation MechanicalDevice "An &%EngineeringComponent in function of
which play role mechanical energetical interactions.")
(subclass MechanicalDevice EngineeringComponent)

(documentation ElectricalDevice "An &%EngineeringComponent in function of
which play role electrical energetical interactions.")
(subclass ElectricalDevice EngineeringComponent)

(documentation FluidPowerDevice "An &%EngineeringComponent in function of
which play role fluid-power energetical interactions.")
(lexicon FluidPowerDevice LexNoun "{fluid power} device")
(subclass FluidPowerDevice EngineeringComponent)

(documentation UnknownDomainDevice "An &%EngineeringComponent for which
the principal physical domain is not specified.")
(lexicon UnknownDomainDevice LexNoun "{unknown domain} device")
(subclass UnknownDomainDevice EngineeringComponent)

(documentation Motor "An actuator intended to deliver mechanical power")
(lexicon Motor LexNoun "motor")
(lexicon Motor LexNoun "engine")
(subclass Motor MechanicalDevice)
(models Transformer Motor)

(documentation Relay "an electro-mechanical device used as a controlled switch")
(lexicon Relay LexNoun "relay")
(lexicon Relay LexNoun "electrical relay")
(subclass Relay ElectricalComponent)
(subclass Relay MechanicalDevice)

(documentation Pendulum "A &%MechanicalDevice of an object mounted so that
it swings freely under the influence of gravity.")
(subclass Pendulum MechanicalDevice)

(documentation Gear "a toothed wheel that engages another toothed
mechanism in order to change the speed or direction of transmitted motion.")
(subclass Gear MechanicalDevice)

(documentation GearTrain "a pair of gears that are used to change speed or
direction of an angular motion; ideal gear train can be modeled using
a transformer.")
(subclass GearTrain MechanicalDevice)
(meronym Gear GearTrain)

(documentation Gearbox "A device composed of several gear-trains used
to change speed and torque of transmitted motion.")
(subclass Gearbox MechanicalDevice)
(meronym GearTrain Gearbox)

(documentation Shaft "A revolving rod that transmits power or motion.")
(subclass Shaft MechanicalDevice)
(meronym Shaft Motor)

(documentation Spring "a metal device that returns to its shape or
position when pushed or pulled or pressed")
(subclass Spring MechanicalDevice)

(documentation Rotor "The rotating armature of a motor or generator.")
(subclass Rotor MechanicalDevice)
(subclass Rotor ElectricalDevice)
(meronym Rotor ElectricalMotor)

(documentation Stator "Stationary part of a motor or generator in or
around which the rotor revolves.")
(subclass Stator MechanicalDevice)
(subclass Stator ElectricalDevice)

(documentation ElectricalMotor "An electrical motor.")
(lexicon ElectricalMotor LexNoun "motor")
(lexicon ElectricalMotor LexNoun "electrical motor")
(subclass ElectricalMotor Motor)
(subclass ElectricalMotor ElectricalDevice)

(documentation DcMotor "A direct-current electrical motor.")
(lexicon DcMotor LexNoun "DC motor")
(subclass DcMotor ElectricalMotor)

(documentation PermanentMagnetDcMotor "A DC motor in which the auxiliary
magnetic field is provided by a permanent magnet.")
(lexicon PermanentMagnetDcMotor LexNoun "{permanent magnet} DC motor")
(subclass PermanentMagnetDcMotor DcMotor)

(documentation SeparatelyExcitedDcMotor "A DC motor in which the auxiliary
magnetic field is provided an active circuit.")
(lexicon SeparatelyExcitedDcMotor LexNoun "{separately excited} DC motor")
(subclass SeparatelyExcitedDcMotor DcMotor)

(documentation InductionMotor "An electrical motor powered by a
three-phase power suply.")
(lexicon InductionMotor LexNoun "induction motor")
(subclass InductionMotor ElectricalMotor)

(documentation Terminal "a point on an electrical device (such as a 
battery) at which electric current enters or leaves ") 

(subclass Terminal ElectricalDevice)
(lexicon Terminal LexNoun "terminal")
(lexicon Terminal LexNoun "electrode")

(meronym Terminal ElectricalComponent)

(documentation Conductor "An &%ElectricalComponent designed to
transmit electricity")
(subclass Conductor ElectricalComponent)
(=>
  (instance ?C Conductor)
  (exists (?S)
    (and
      (instance ?S ConductorSubstance)
      (material ?S ?C))))

(documentation Resistor "An &%ElectricalComponent that resists the flow of
electrical current.")
(subclass Resistor ElectricalComponent)
(=>
  (instance ?I Resistor)
  (exists (?S)
    (and
      (instance ?I InsulatorSubstance)
      (material ?I ?C))))
      
(documentation Capacitor "An &%ElectricalComponent characterized by its
capacitance.")
(subclass Capacitor ElectricalComponent)

(documentation Inductor "An &%ElectricalComponent that introduces inductance
into a circuit.")
(subclass Inductor ElectricalComponent)

(documentation TransformerDevice "An &%ElectricalDevice by which alternating 
current of one voltage is changed to another voltage.")
(lexicon TransformerDevice LexNoun "transformer")
(subclass TransformerDevice ElectricalDevice)

(documentation PowerSource "A source of electrical power.")
(lexicon PowerSource LexNoun "power source")
(lexicon PowerSource LexNoun "source {of power}")
(subclass PowerSource ElectricalDevice)

(documentation SinglePhasePowerSource "A &%PowerSource of single phase
alternating current.")
(lexicon SinglePhasePowerSource LexNoun "single-phase {power source}")
(subclass SinglePhasePowerSource PowerSource)

(documentation ThreePhasePowerSource "A &%PowerSource of three phase
alternating current.")
(lexicon ThreePhasePowerSource LexNoun "three-phase {power source}")
(subclass ThreePhasePowerSource PowerSource)

(documentation DCPowerSource "A direct-current &%PowerSource.")
(lexicon DCPowerSource LexNoun "DC {power source}")
(subclass DCPowerSource PowerSource)

(documentation Battery "A portable &%DCPowerSource that converts chemical
into mechanical energy.")
(subclass Battery DCPowerSource)

(documentation Cylinder "A &%FluidPowerDevice designed to transform
fluid-power energy into mechanical translatory energy.")
(lexicon Cylinder LexNoun "cylinder")
(lexicon Cylinder LexNoun "hydraulic cylinder")
(subclass Cylinder MechanicalDevice)
(subclass Cylinder FluidPowerDevice)
(models Gyrator Cylinder)

(documentation Valve "A hydraulic valve.")
(lexicon Valve LexNoun "valve")
(subclass Valve MechanicalDevice)
(subclass Valve FluidPowerDevice)

(documentation DirectionalControlValve "Valve that controls the direction
of flow of a fluid")
(lexicon DirectionalControlValve LexNoun "{directional control} valve")
(subclass DirectionalControlValve Valve)

(documentation SpoolValve "a directional control valve that uses a spool
to control the direction of flow")
(subclass SpoolValve DirectionalControlValve)

(documentation PressureControlValve "valve that controls the pressure in
a fluid")
(lexicon PressureControlValve LexNoun "{pressure control} valve")
(subclass PressureControlValve Valve)

(documentation ReliefValve "Most common type of a presure control valve;
it consists of a piston that is retained on its seat by a spring")
(lexicon ReliefValve LexNoun "relief valve")
(subclass ReliefValve PressureControlValve)

(documentation VolumeControlValve "valve that controls the flow rate of
a fluid")
(lexicon VolumeControlValve LexNoun "{volume control} valve")
(subclass VolumeControlValve Valve)

(documentation ElectricalComponent "A discrete &%ElectricalDevice for
general usage, such as resistors, capacitors, diodes, transistors etc.")
(subclass ElectricalComponent ElectricalDevice)

(documentation ElectricalCircuit "A complex &%ElectricalDevice consisting
of several mutually interconnected electrical components.")
(subclass ElectricalCircuit ElectricalDevice)

(documentation Amplifier "Electronic equipment that increases strength of
signals passing through it; it can be modeled using a controlled
source, or an operational amplifier.")
(lexicon Amplifier LexNoun "amplifier")
(subclass Amplifier ElectricalCircuit)

(documentation FrequencyFilter "Alters the frequency spectrum of signals
passing through it")
(lexicon FrequencyFilter LexNoun "frequency filter")
(subclass FrequencyFilter ElectricalCircuit)

(documentation InvertingAmplifier "An &%Amplifier that changes the polarity	
of the input signal.")
(lexicon InvertingAmplifier LexNoun "inverting amplifier")
(subclass InvertingAmplifier Amplifier)

(documentation NoninvertingAmplifier "An &%Amplifier that does not change the polarity	
of the input signal.")
(lexicon Amplifier LexNoun "non-inverting amplifier")
(subclass NoninvertingAmplifier Amplifier)

(documentation Rectifier "electrical device that transforms alternating
into direct current.")
(lexicon Rectifier LexNoun "rectifier")
(subclass Rectifier ElectricalCircuit)

(documentation SinglePhaseRectifier "A &%Rectifier of single phase AC
voltage.")
(lexicon SinglePhaseRectifier LexNoun "single-phase rectifier")
(subclass SinglePhaseRectifier Rectifier)

(documentation HalfWaveSinglePhaseRectifier "A &%SinglePhaseRectifier of
that exploits only one polarity of the input power source.")
(lexicon HalfWaveSinglePhaseRectifier LexNoun "half-wave single-phase rectifier")
(subclass HalfWaveSinglePhaseRectifier SinglePhaseRectifier)

(documentation BridgeSinglePhaseRectifier "A &%SinglePhaseRectifier of
that exploits both polarities of the input power source.")
(lexicon BridgeSinglePhaseRectifier LexNoun "bridge single-phase rectifier")
(subclass BridgeSinglePhaseRectifier SinglePhaseRectifier)

(documentation DoublerSinglePhaseRectifier "A &%SinglePhaseRectifier of
that exploits both polarities of the input power source.")
(lexicon DoublerSinglePhaseRectifier LexNoun "doubler rectifier")
(subclass DoublerSinglePhaseRectifier SinglePhaseRectifier)

(documentation ThreePhaseRectifier "A &%Rectifier of single phase AC
voltage.")
(lexicon ThreePhaseRectifier LexNoun "three-phase rectifier")
(subclass ThreePhaseRectifier Rectifier)

(documentation SemiconductorComponent "An electrical device that exploits
properties of semiconductors.")
(subclass SemiconductorComponent ElectricalComponent)

(documentation OperationalAmplifier "A complex semiconductor device with
behaviour similar to the &%IdealOperationalAmplifier.")
(lexicon OperationalAmplifier LexNoun "operational amplifier")
(subclass OperationalAmplifier ElectricalCircuit)

(documentation Diode "A semiconductor device that consists of a p-n
junction; it is used e.g. in rectifiers or demodulators.")
(subclass Diode SemiconductorComponent)
(lexicon Diode LexNoun "diode")

(documentation ZenerDiode " ")
(subclass ZenerDiode Diode)
(lexicon ZenerDiode LexNoun "zener diode")

(documentation LED "light emitted at a p-n junction is proportional 
to the bias current; color depends on the material used")
(subclass LED Diode)
(lexicon LED LexNoun "LED")
(lexicon LED LexNoun "light-emitting diode")

(documentation Transistor "A semiconductor device capable of amplification
or switching.")
(lexicon Transistor LexNoun "transistor")
(subclass Transistor SemiconductorComponent)

(documentation Base "the part of a transistor that separates the emitter 
from the collector")
(subclass Base Terminal)
(lexicon Base LexNoun "base")
(meronym Base BjtTransistor)
(documentation Collector "the electrode in a transistor through which a 
primary flow of carriers leaves the inter-electrode region")
(subclass Collector Terminal)
(lexicon Collector LexNoun "collector")
(meronym Collector BjtTransistor)
(documentation Emitter "the electrode in a transistor where electrons originate")
(subclass Emitter Terminal)
(lexicon Emitter LexNoun "emitter")
(meronym Emitter BjtTransistor)

(documentation BjtTransistor "Bipolar transistor")
(lexicon BjtTransistor LexNoun "BJT transistor")
(subclass BjtTransistor Transistor)

(documentation FetTransistor "Field-effect transistor.")
(lexicon FetTransistor LexNoun "FET transistor")
(subclass FetTransistor Transistor)

(documentation JfetTransistor "Junction field-effect transistor.")
(lexicon JfetTransistor LexNoun "JFET transistor")
(subclass JfetTransistor FetTransistor)

(documentation MosfetTransistor "Metal-oxyde semiconductor field-effect
transistor.")
(lexicon MosfetTransistor LexNoun "MOSFET transistor")
(subclass MosfetTransistor FetTransistor)

(documentation NpnTransistor "A &%BjtTransistor with N-P-N junctions.")
(lexicon NpnTransistor LexNoun "NPN transistor")
(subclass NpnTransistor BjtTransistor)

(documentation PnpTransistor "A &%BjtTransistor with P-N-P junctions.")
(lexicon PnpTransistor LexNoun "PNP transistor")
(subclass PnpTransistor BjtTransistor)

(documentation Thyristor "a &%SemiconductorComponent that consists of three
p-n junctions; it is used e.g. in controlled rectifiers.")
(lexicon Thyristor LexNoun "thyristor")
(lexicon Thyristor LexNoun "thyrister")
(subclass Thyristor SemiconductorComponent)

(documentation Controller "a mechanism that controls the operation of some
device")
(subclass Controller UnknownDomainDevice)

(lexicon Vehicle LexNoun "vehicle")

(documentation Car "4-wheeled motor vehicle; usually propelled by
an internal combustion engine")
(lexicon Car LexNoun "car")
(subclass Car Vehicle)

(documentation Truck "an automotive vehicle suitable for hauling")
(lexicon Truck LexNoun "truck")
(subclass Truck Car)

;;;;;;;;;;;;;;;;;
;;  METHODS    ;;
;;;;;;;;;;;;;;;;;

(documentation Method "a way of doing something, esp. a systematic one;
implies an orderly logical arrangement (usually in steps).")
(subclass Method Procedure)

(documentation MathematicalMethod "A &%Method that involves using
mathematical aparatus.")
(subclass MathematicalMethod Method)

(documentation FourierAnalysis "Computing a Fourier series for given
periodic function")
(lexicon FourierAnalysis LexNoun "Fourier analysis")
(subclass FourierAnalysis MathematicalMethod)

(documentation FourierSynthesis "Reconstruction of a periodic function
from its Fourier series representation.")
(lexicon FourierSynthesis LexNoun "Fourier synthesis")
(subclass FourierSynthesis MathematicalMethod)

(documentation Discretization "A method of converting continuous problem
to a discrete one, loaded by some discretization error.")
(lexicon Discretization LexNoun "discretization")
(lexicon Discretization LexVerb "discretize")
(subclass Discretization MathematicalMethod)

(documentation Technique "A practical &%Method or art applied to some
particular task")
(subclass Technique Method)

(documentation ElectricalEngineeringMethod "A &%Method used in electrical
engineering for designing and constructing electrical devices.")
(lexicon ElectricalEngineeringMethod LexNoun "{electrical engineering} method")
(subclass ElectricalEngineeringMethod Method)

(documentation Pwm "Pulse-width modulation technique")
(subclass Pwm ElectricalEngineeringMethod)

(documentation EngineersSubprocess "a class of subprocesses that are needed to
design and operate an engineering system")
(subclass EngineersSubprocess IntentionalProcess)

(documentation EngineersProcess "a process of designing, manufacturing and
operating of an engineering system involving all stages of the life cycle.")
(subclass EngineersProcess EngineersSubprocess)

(documentation SystemSpecification "a process of specifying requirements
on a system")
(lexicon SystemSpecification LexNoun "system specification")
(subclass SystemSpecification EngineersSubprocess)

(documentation SystemDesign "a process of designing an engineering system")
(lexicon SystemDesign LexNoun "system design")
(lexicon SystemDesign LexNoun "{design of} system")
(subclass SystemDesign EngineersSubprocess)

(documentation ControlDesignMethod "A class of control design method")
(subclass ControlDesignMethod Method)

(documentation RootLocus "Root-locus control design method")
(subclass RootLocus ControlDesignMethod)
(lexicon RootLocus LexNoun "{root locus}")

;;;;;;;;;;;;;;;;;
;;  LAWS       ;;
;;;;;;;;;;;;;;;;;

(documentation Law "A generalization based on recurring facts or events 
(in science or mathematics etc)")
(subclass Law Proposition)

(documentation NewtonsLaw "One of three basic laws of classical mechanics.")
(lexicon NewtonsLaw LexNoun "Newton's law")
(subclass NewtonsLaw Law)

(documentation NewtonsFirstLaw "A body remains at rest or in motion with
a constant velocity unless acted upon by an external force")
(lexicon NewtonsFirstLaw LexNoun "{Newton's first} law")
(subclass NewtonsFirstLaw NewtonsLaw)

(documentation NewtonsSecondLaw "The rate of change of momentum is
proportional to the imposed force and goes in the direction of
the force")
(lexicon NewtonsSecondLaw LexNoun "{Newton's second} law")
(subclass NewtonsSecondLaw NewtonsLaw)

(documentation NewtonsThirdLaw "Action and reaction are equal and opposite.")
(lexicon NewtonsThirdLaw LexNoun "{Newton's third} law")
(subclass NewtonsThirdLaw NewtonsLaw)

(documentation MultipolePostulate "One of the postulates of the multipole
modeling theory.")
(subclass MultipolePostulate Law)

(documentation PostulateOfContinuity "The sum of through variables of
poles of multipole section is equal to zero.")
(subclass PostulateOfContinuity MultipolePostulate)
(lexicon PostulateOfContinuity LexNoun "postulate {of continuity}")

;;;;;;;;;;;;;;;;;;
;;  SUBSTANCES  ;;
;;;;;;;;;;;;;;;;;;

(documentation ConductorSubstance "A &%Substance that readily conducts
electricity.")
(lexicon ConductorSubstance LexNoun "conductor")
(subclass ConductorSubstance Substance)

(documentation InsulatorSubstance "a &%Substance such as glass or
porcelain with negligible electrical conductivity.")
(lexicon InsulatorSubstance LexNoun "insulator")
(subclass InsulatorSubstance Substance)

;;;;;;;;;;;;;;;;;
;;  COMPUTERS  ;;
;;;;;;;;;;;;;;;;;

(lexicon Computer LexNoun "computer")

(lexicon ComputerProgram LexNoun "{computer program}")
(lexicon ComputerProgram LexNoun "program")
(subclass ComputerProgram ITAgent)
(subclass Human ITAgent)

(documentation ITAgent "agent capable of performing ITProcess")
(subclass ITAgent Agent)

(documentation ITProcess "A process performed on a computer by a human
operator.")
(subclass ITProcess IntentionalProcess)
(documentation "&%Each ITProcess is performed by a human on a computer.")
(=>
	(instance ?PROCESS ITProcess)
	(exists (?AGENT ?PATIENT)
		(and
			(agent ?PROCESS ?AGENT)
			(patient ?PROCESS ?PATIENT)
			(instance ?AGENT ITAgent)
			(instance ?PATIENT Computer))))

(documentation Installation "The process of deploying an application
on a computer.")
(lexicon Installation LexNoun "installation")
(lexicon Installation LexVerb "install")
(subclass Installation ITProcess)

(documentation UnInstallation "The process of removing of an installed
application from a computer.")
(lexicon UnInstallation LexNoun "uninstallation")
(lexicon UnInstallation LexVerb "uninstall")
(subclass UnInstallation ITProcess)

(lexicon OperatingSystem LexNoun "operating system")

(documentation FileSystem "file system")
(subclass FileSystem Group)
(lexicon FileSystem LexNoun "{file system}")

(lexicon ComputerDirectory LexNoun "folder")
(lexicon ComputerDirectory LexNoun "directory")

(subclass ComputerFile ContentBearingObject)
(lexicon ComputerFile LexNoun "file")

(documentation "file systems contain only files and folders")
(=>
	(and
		(instance ?FS FileSystem)
		(member ?M ?FS))
	(instance ?M ComputerFile))

(documentation "folders contain only files and folders")
(=>
	(and
		(instance ?F ComputerDirectory)
		(member ?M ?F))
	(instance ?M ComputerFile))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  APPLICATION DOMAIN TAXONOMY  ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation ApplicationDomain "set of application domains (medicine, physics, etc)")
(subclass ApplicationDomain InternalAttribute)
(lexicon ApplicationDomain LexNoun "application domain")
(lexicon ApplicationDomain LexNoun "domain")
(lexicon ApplicationDomain LexNoun "domain of interest")

(documentation ScienceDomain "science (natural sciences, engineering, medicine...)")
(subclass ScienceDomain ApplicationDomain)
(lexicon ScienceDomain LexNoun "science domain")
(lexicon ScienceDomain LexNoun "science")

(documentation EngineeringDomain "engineering (mechanical, electrical)")
(subclass EngineeringDomain ScienceDomain)
(lexicon EngineeringDomain LexNoun "engineering")
(lexicon EngineeringDomain LexNoun "engineering domain")

(documentation MechanicalEngineeringDomain "domain involving mechanical engineering")
(subclass MechanicalEngineeringDomain EngineeringDomain)
(lexicon MechanicalEngineeringDomain LexNoun "mechanical engineering")

(documentation FluidPowerDomain "fluid power (hydraulic) systems")
(subclass FluidPowerDomain MechanicalEngineeringDomain)
(lexicon FluidPowerDomain LexNoun "fluid-power")
(lexicon FluidPowerDomain LexNoun "fluid-power systems")

(documentation ElectricalEngineeringDomain "domain involving electrical engineering")
(subclass ElectricalEngineeringDomain EngineeringDomain)
(lexicon ElectricalEngineeringDomain LexNoun "electrical engineering")

(documentation CircuitTheoryDomain "circuit theory")
(subclass CircuitTheoryDomain ElectricalEngineeringDomain)
(subclass CircuitTheoryDomain PhysicsDomain)
(lexicon CircuitTheoryDomain LexNoun "circuit theory")

(documentation ControlDomain "control")
(subclass ControlDomain EngineeringDomain)
(lexicon ControlDomain LexNoun "control")

(documentation ElectroMechanicalDevicesDomain "electrical drives (motors)")
(subclass ElectroMechanicalDevicesDomain ElectricalEngineeringDomain)
(subclass ElectroMechanicalDevicesDomain MechanicalEngineeringDomain)
(lexicon ElectroMechanicalDevicesDomain LexNoun "electro-mechanical devices")

(documentation ElectricalDrivesDomain "electrical drives (motors)")
(subclass ElectricalDrivesDomain ElectroMechanicalDevicesDomain)
(lexicon ElectricalDrivesDomain LexNoun "electrical drives")

(documentation RelaysDomain "relays")
(subclass RelaysDomain ElectroMechanicalDevicesDomain)
(lexicon RelaysDomain LexNoun "relays")

(documentation ElectronicsDomain "electronic circuits - electrical circuits 
containing complex semiconductor components ")
(subclass ElectronicsDomain ElectricalEngineeringDomain)
(lexicon ElectronicsDomain LexNoun "electronics")

(documentation PowerElectronicsDomain "power electronic circuits (power supplies etc.)")
(subclass PowerElectronicsDomain ElectronicsDomain)
(lexicon PowerElectronicsDomain LexNoun "power electronics")

(documentation NaturalSciencesDomain "natural sciences (mathematics, physics)")
(subclass NaturalSciencesDomain ScienceDomain)
(lexicon NaturalSciencesDomain LexNoun "natural sciences")

(documentation MathematicsDomain "mathematics")
(subclass MathematicsDomain NaturalSciencesDomain)
(lexicon MathematicsDomain LexNoun "mathematics")

(documentation PhysicsDomain "physics")
(subclass PhysicsDomain NaturalSciencesDomain)
(lexicon PhysicsDomain LexNoun "physics")

(documentation MechanicsDomain "basic (theoretic) mechanics")
(subclass MechanicsDomain PhysicsDomain)
(lexicon MechanicsDomain LexNoun "mechanics")

(documentation AstronomyDomain "astronomy")
(subclass AstronomyDomain PhysicsDomain)
(lexicon AstronomyDomain LexNoun "astronomy")

;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  DOCUMENT CATEGORIES  ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;

(documentation WebDocument "knowledge base document - a document in natural language 
representing one piece of knowledge in the knowledge base")
(subclass WebDocument ContentBearingObject)
(lexicon WebDocument LexNoun "knowledge base document")
(lexicon WebDocument LexNoun "document")
(lexicon WebDocument LexNoun "web document")

(documentation WebDocumentAttribute "knowledge base document attribute")
(subclass WebDocumentAttribute InternalAttribute)
(lexicon WebDocumentAttribute LexNoun "knowledge base document attribute")

(=>
	(and
		(instance ?ATTR WebDocumentAttribute)
		(attribute ?OBJ ?ATTR))
	(instance ?OBJ WebDocument))

(documentation DocumentCategory "document category")
(subclass DocumentCategory WebDocumentAttribute)
(lexicon DocumentCategory LexNoun "document category")

(documentation SolvedExampleCategory "solved example")
(instance SolvedExampleCategory DocumentCategory)
(lexicon SolvedExampleCategory LexNoun "solved example")

(documentation ArticleCategory "article")
(instance ArticleCategory DocumentCategory)
(lexicon ArticleCategory LexNoun "article")

(documentation TutorialCategory "tutorial")
(instance TutorialCategory DocumentCategory)
(lexicon TutorialCategory LexNoun "tutorial")

(documentation LibraryModelCategory "library model")
(instance LibraryModelCategory DocumentCategory)
(lexicon LibraryModelCategory LexNoun "library model")
