;;  copyright 2002 (c) Teknowledge
;;
;;  This program is free software; you can redistribute it and/or
;;  modify it under the terms of the GNU Lesser General Public
;;  License as published by the Free Software Foundation; either
;;  version 2.1 of the License, or (at your option) any later version.
;;
;;  This program is distributed in the hope that it will be useful,
;;  but WITHOUT ANY WARRANTY; without even the implied warranty of
;;  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;;  Lesser General Public License for more details.
;;
;;  You should have received a copy of the GNU Lesser General Public
;;  License along with this program; if not, write to the Free Software
;;  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
;;
;;  This file is an ontology of weapons of mass destruction, including chemical 
;;  and biological weapons, as well as radiological weapons.  The sources used to 
;;  develop this formal content are listed below.
;;
;;  Miller, Judith, Engelberg, Stephen, and Broad, William (2002), "Germs: 
;;  Biological Weapons and America's Secret War", Touchstone/Simon and Schuster. 
;;
;;  Norris, John and Fowler, Will (1997), "NBC:  Nuclear, Biological & Chemical 
;;  Warfare on the Modern Battlefield", Brassey's Inc.
;;
;;  Marris, Timothy C., Maynard, Robert L., and Sidell, Frederick R. (1996), 
;;  "Chemical Warfare Agents:  Toxicology and Treatment", John Wiley & Sons.
;;
;;  Centers for Disease Control web site.  http://www.cdc.gov/.
;;
;;  Federation of American Scientists Chemical and Biological Arms Control 
;;  Program web site.  http://www.fas.org/bwc/index.html.
;;
;;  Center for Nonproliferation Studies (CNS) web site.  http://cns.miis.edu/index.htm.  


(subclass BiochemicalAttack ViolentContest)
(documentation BiochemicalAttack "&%ViolentContests in which a &%BiochemicalWeapon 
is used against an &%Organism.")

(=> 
    (and 
      (instance ?WEAPON WeaponOfMassDestruction) 
      (instrument ?ATTACK ?WEAPON) 
      (instance ?ATTACK BiochemicalAttack)) 
    (instance ?WEAPON BiochemicalWeapon))

(=> 
    (and 
      (instance ?WEAPON BiochemicalWeapon) 
      (possesses ?AGENT ?WEAPON))
    (capability BiochemicalAttack agent ?AGENT)) 

(=> 
    (and 
      (instance ?ATTACK ViolentContest) 
      (instrument ?ATTACK ?WEAPON) 
      (instance ?WEAPON BiochemicalWeapon)) 
    (instance ?ATTACK BiochemicalAttack))

(=> 
    (and 
      (instance ?ATTACK BiochemicalAttack) 
      (patient ?ATTACK ?OBJ))
    (instance ?OBJ Organism))

(=>
   (instance ?ATTACK BiochemicalAttack)
   (hasPurpose ?ATTACK 
      (exists (?DAMAGE ?ORGANISM) 
         (and 
            (instance ?DAMAGE Damaging) 
            (patient ?DAMAGE ?ORGANISM) 
            (causes ?ATTACK ?DAMAGE)
            (instance ?Organism Organism)))))

(partition BiochemicalAgent BiologicalAgent ChemicalAgent)

(subclass AerosolizedAgent BiochemicalAgent)
(documentation AerosolizedAgent "A &%BiochemicalAgent that has been divided into 
particles so minute that they can be easily dispersed in the air and inhaled by 
&%Humans or &%Animals.  &%AerosolizedAgents tend to be more lethal and to affect 
a larger area.")

(disjoint BiochemicalWeapon RadioactiveWeapon)
(partition BiochemicalWeapon BiologicalWeapon ChemicalWeapon)

(=>
   (instance ?WEAPON BiochemicalWeapon)
   (exists (?AGENT)
      (and
         (instance ?AGENT BiochemicalAgent)
         (part ?AGENT ?WEAPON))))

(=>
   (instance ?WEAPON BiologicalWeapon)
   (exists (?AGENT)
      (and
         (instance ?AGENT BiologicalAgent)
         (part ?AGENT ?WEAPON))))

(partition BiologicalAgent Toxin ToxicOrganism)

(subclass Toxin BiologicalAgent)
(subclass Toxin BiologicallyActiveSubstance)
(documentation Toxin "&%BiologicalAgents that are a toxic 
&%BiologicallyActiveSubstance produced by an &%Organism or 
that are the synthetic analogue of a toxic &%BiologicallyActiveSubstance 
produced by an &%Organism.") 

(=>
   (instance ?SUBSTANCE Toxin)
   (exists (?ORGANISM ?PROCESS)
      (and
         (instance ?ORGANISM ToxicOrganism)
         (instance ?PROCESS BiologicalProcess)
         (instrument ?PROCESS ?ORGANISM)
         (or
            (result ?PROCESS ?SUBSTANCE)
            (exists (?RESULT)
               (and
                  (result ?PROCESS ?RESULT)
                  (copy ?SUBSTANCE ?RESULT)))))))

(=>
   (and
      (instance ?DAMAGE Damaging)
      (instrument ?DAMAGE ?SUBSTANCE)
      (instance ?SUBSTANCE BiologicallyActiveSubstance))
   (instance ?SUBSTANCE Toxin))

(subclass ToxicOrganism Organism)
(subclass ToxicOrganism BiologicalAgent)
(documentation ToxicOrganism "The &%Class of &%Organisms which are 
poisonous to other &%Organisms.")

(=>
   (instance ?ORGANISM ToxicOrganism)
   (exists (?SUBSTANCE)
      (and 
         (instance ?SUBSTANCE Toxin)
         (part ?SUBSTANCE ?ORGANISM))))

(subclass BacterialAgent ToxicOrganism)
(subclass BacterialAgent Bacterium)
(biochemicalAgentDelivery BacterialAgent Breathing)
(biochemicalAgentDelivery BacterialAgent Touching)
(documentation BacterialAgent "&%BiologicalAgents that are instances 
of &%Bacterium.")

(=>
   (instance ?BACTERIUM Bacterium)
   (exists (?NUMBER)
      (and
         (width ?BACTERIUM (MeasureFn ?NUMBER Meter))
         (greaterThanOrEqualTo ?NUMBER 0.000001)
         (lessThanOrEqualTo ?NUMBER 0.000002)))) 

(subclass FungalAgent ToxicOrganism)
(subclass FungalAgent Fungus)
(documentation FungalAgent "Any &%BiologicalAgent that is also a &%Fungus.")

(subclass Mycotoxin Toxin)
(subclass Mycotoxin BodySubstance)
(documentation Mycotoxin "A &%Toxin that is produced by a &%FungalAgent.")

(=>
   (instance ?SUBSTANCE Mycotoxin)
   (exists (?FUNGUS ?PROCESS)
      (and
         (instance ?FUNGUS FungalAgent)
         (instance ?PROCESS BiologicalProcess)
         (instrument ?PROCESS ?ORGANISM)
         (result ?PROCESS ?SUBSTANCE))))

(subclass ViralAgent ToxicOrganism)
(subclass ViralAgent Virus)
(documentation ViralAgent "&%BiologicalAgents that are also a &%Virus.")

(subclass GeneticallyEngineeredOrganism Organism)
(documentation GeneticallyEngineeredOrganism "The class of &%Organisms that 
are not found originally in nature, but are produced in a laboratory by 
manipulating the genes of naturally occurring organisms.")

(=>
   (instance ?ORGANISM GeneticallyEngineeredOrganism)
   (exists (?PROCESS)
      (and
         (instance ?PROCESS IntentionalProcess)
         (result ?PROCESS ?ORGANISM))))

(subclass RickettsialAgent BacterialAgent)
(biologicalAgentCarrier RickettsialAgent Mammal)
(biologicalAgentCarrier RickettsialAgent Arthropod)
(documentation RickettsialAgent "&%BiologicalAgents that are rickettsial 
organisms, i.e. gram-negative bacteria that infect mammals and arthropods.")

(subclass RickettsiaRickettsii RickettsialAgent)
(biologicalAgentCarrier RickettsiaRickettsii Arthropod)
(biochemicalAgentSyndrome RickettsiaRickettsii RockyMountainSpottedFever)
(documentation RickettsiaRickettsii "The &%Bacterium that causes 
&%RockyMountainSpottedFever.")

(instance RockyMountainSpottedFever BacterialDisease)
(instance RockyMountainSpottedFever LifeThreateningDisease)
(diseaseMortality RockyMountainSpottedFever 0.04)
(diseaseTreatment RockyMountainSpottedFever OralAntibiotic Ingesting)
(documentation RockyMountainSpottedFever "A very serious disease that is 
caused &%RickettsiaRickettsii, which is carried by ticks.  The most 
distinctive symptom of the disease is a rash of black dots.") 

(subclass RickettsiaProwazekii RickettsialAgent)
(biologicalAgentCarrier RickettsiaProwazekii Arthropod) 
(biochemicalAgentSyndrome RickettsiaProwazekii LouseBorneTyphus)
(documentation RickettsiaProwazekii "The &%Bacterium that causes &%LouseBorneTyphus.")

(instance LouseBorneTyphus BacterialDisease)
(diseaseIncubation LouseBorneTyphus (MeasureFn 1 WeekDuration) (MeasureFn 2 WeekDuration)) 
(diseaseTreatment LouseBorneTyphus OralAntibiotic Ingesting)
(documentation LouseBorneTyphus "The only rickettsial disease which is capable of causing 
widespread human epidemics.  The initial symptoms of the disease are flu-like, but on the 
fifth or sixth day of infection a macular eruption appears on the upper trunk of the body 
and then spreads to the rest of the body.")

(subclass BacillusAnthracis BacterialAgent)
(biochemicalAgentSyndrome BacillusAnthracis Anthrax)
(biochemicalAgentDelivery BacillusAnthracis Breathing)
(biochemicalAgentDelivery BacillusAnthracis Ingesting)
(biochemicalAgentDelivery BacillusAnthracis Poking)
(biochemicalAgentDelivery BacillusAnthracis Touching)
(biologicalAgentCarrier BacillusAnthracis HoofedMammal)
(documentation BacillusAnthracis "The &%Bacterium which causes the disease 
&%Anthrax. Humans may become infected with Anthrax via contamination of a 
wound or by inhaling the &%Bacterium.  When it is inhaled, the disease is 
often fatal if not treated early (see &%InhalationalAnthrax).") 

(subclass AerosolizedBacillusAnthracis BacillusAnthracis)
(subclass AerosolizedBacillusAnthracis AerosolizedAgent)
(biochemicalAgentDelivery AerosolizedBacillusAnthracis Breathing)
(biochemicalAgentSyndrome AerosolizedBacillusAnthracis InhalationalAnthrax) 
(documentation AerosolizedBacillusAnthracis "Also known as weaponized anthrax.  These 
are spores of &%BacillusAnthracis that have been separated to the point that they can 
be dispersed in the air and easily inhaled by crowds.")

(subclass Vollum1B AerosolizedBacillusAnthracis)
(documentation Vollum1B "A highly lethal, weaponized form of &%BacillusAnthracis 
developed by the United States government.")

(subclass Batrachotoxin Toxin)
(subclass Batrachotoxin BodySubstance)
(lethalDose Batrachotoxin (PerFn (MeasureFn 2.0 (MicroFn Gram)) (MeasureFn 1.0 (KiloFn Gram))))
(documentation Batrachotoxin "A &%Toxin produced by some species of Frogs.")

(=>
   (secretesToxin ?FROG Batrachotoxin)
   (subclass ?FROG Amphibian))

(subclass ClostridiumBotulinum BacterialAgent)
(secretesToxin ClostridiumBotulinum BotulinumToxin)
(documentation ClostridiumBotulinum "The &%Bacterium that produces 
&%BotulinumToxin.")

(subclass BotulinumToxin Toxin)
(subclass BotulinumToxin BodySubstance)
(subclass BotulinumToxin LifeThreateningAgent)
(lethalDose BotulinumToxin (PerFn (MeasureFn 0.001 (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(biochemicalAgentDelivery BotulinumToxin Ingesting)
(biochemicalAgentSyndrome BotulinumToxin Botulism)
(documentation BotulinumToxin "A &%Toxin produced by the bacterium 
&%ClostridiumBotulinum.  It paralyzes muscles if ingested, and one 
billionth of a pound is sufficient to cause death.")

(instance Botulism BacterialDisease)
(instance Botulism LifeThreateningDisease)
(instance Botulism VaccinatableDisease)
(diseaseSymptom Botulism Paralysis)
(diseaseIncubation Botulism (MeasureFn 2 HourDuration) (MeasureFn 8 DayDuration))
(documentation Botulism "A paralytic disease caused by &%BotulinumToxin, 
a &%Toxin produced by the bacterium &%ClostridiumBotulinum.") 

(subclass ClostridiumPerfringens BacterialAgent)
(secretesToxin ClostridiumPerfringens EpsilonToxin) 
(biochemicalAgentDelivery ClostridiumPerfringens Ingesting)
(documentation ClostridiumPerfringens "&%Bacterium often found in the 
intestines of &%Animals.  Ingesting this &%Bacterium can result in 
Perfringens food poisoning, which can attack internal organs and lead 
to gangrene.")

(subclass EpsilonToxin Toxin)
(subclass EpsilonToxin BodySubstance)
(biochemicalAgentDelivery EpsilonToxin Ingesting)
(documentation EpsilonToxin "A &%Toxin produced by the bacterium 
&%ClostridiumPerfringens that causes a mild form of food poisoning 
that lasts one to two days.")

(subclass BurkholderiaMallei BacterialAgent)
(biologicalAgentCarrier BurkholderiaMallei Mammal)
(biochemicalAgentDelivery BurkholderiaMallei Poking)
(biochemicalAgentSyndrome BurkholderiaMallei Glanders)
(diseaseTreatment BurkholderiaMallei Ciprofloxacin Ingesting)
(documentation BurkholderiaMallei "The &%Bacterium that causes the disease &%Glanders.  
It is transmitted to humans by direct contact with infected animals.  The bacteria enter 
the body through the skin and the mucosal surfaces of the eyes and nose.")

(instance Glanders BacterialDisease)
(diseaseSymptom Glanders Fever)
(documentation Glanders "A disease caused by &%BurkholderiaMallei.  Primary symptoms 
of &%Glanders include fever, muscle aches, chest pain, muscle tightness, and headache.  
Additional symptoms include tearing of the eyes, light sensitivity, and diarrhea.  
There is no vaccine for this disease.") 

(subclass BurkholderiaPseudomallei BacterialAgent)
(biologicalAgentCarrier BurkholderiaPseudomallei Mammal)
(biochemicalAgentDelivery BurkholderiaPseudomallei Poking)
(biochemicalAgentDelivery BurkholderiaPseudomallei Breathing)
(biochemicalAgentDelivery BurkholderiaPseudomallei Ingesting)
(biochemicalAgentSyndrome BurkholderiaPseudomallei Melioidosis) 
(documentation BurkholderiaPseudomallei "Formerly known as Pseudomonas Pseudomallei, this 
is the &%Bacterium that causes &%Melioidosis.") 

(instance Melioidosis BacterialDisease)
(documentation Melioidosis "Also known as Whitmore's disease, it is similar to &%Glanders.  
Unlike &%Glanders, however, the disease is found predominantly in Southeast Asia.  The 
disease may be a localized infection or it may involve the bloodstream, the lungs or other 
organs of the body.")   

(subclass BrucellaBacterium BacterialAgent)
(biochemicalAgentSyndrome BrucellaBacterium Brucellosis)
(documentation BrucellaBacterium "The &%Bacterium which is responsible for the 
disease &%Brucellosis.  This class covers Brucella Abortus (which infects humans 
and cattle), Brucella Melitensis (which infects sheep, goats, and humans), and 
Brucella Suis (which infects pigs).") 

(instance Brucellosis BacterialDisease)
(diseaseSymptom Brucellosis Fever)
(documentation Brucellosis "Infectious disease caused by &%BrucellaBacterium.  It is 
also known as undulant fever and malta fever, depending on the strain of 
&%BrucellaBacterium that is reponsible for the infection.")

(subclass YersiniaPestis BacterialAgent)
(subclass YersiniaPestis LifeThreateningAgent)
(biochemicalAgentSyndrome YersiniaPestis Plague)
(biologicalAgentCarrier YersiniaPestis Rodent)
(biologicalAgentCarrier YersiniaPestis Insect)
(documentation YersiniaPestis "The &%Bacterium that causes &%Plague.")

(instance Plague BacterialDisease)
(instance Plague VaccinatableDisease)
(diseaseSymptom Plague Fever)
(documentation Plague "The disease caused by the &%Bacterium &%YersiniaPestis.  
&%Plague has two forms depending on the location of the infection within the 
body, &%BubonicPlague and &%PneumonicPlague.")

(subAttribute BubonicPlague Plague)
(diseaseIncubation BubonicPlague (MeasureFn 2 DayDuration) (MeasureFn 6 DayDuration))
(documentation BubonicPlague "A variant of the &%Plague which results in large 
swellings, called buboes.")

(subAttribute PneumonicPlague Plague)
(diseaseIncubation PneumonicPlague (MeasureFn 3 DayDuration) (MeasureFn 4 DayDuration))
(documentation PneumonicPlague "Also known as pulmonary plague, a variant 
of the &%Plague which attacks the lungs and is spread between humans by oral 
bodily fluids.")

(subclass RicinToxin Toxin)
(subclass RicinToxin Protein)
(lethalDose RicinToxin (PerFn (MeasureFn 3.0 (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(documentation RicinToxin "An extremely toxic protein found in the castor 
bean plant (ricinus communis).  It is 200 times more toxic than cyanide, 
and it has no known antidote, and it causes vomiting, high fever, weakness, 
and death.") 

(subclass AbrinToxin Toxin)
(subclass AbrinToxin Protein)
(lethalDose AbrinToxin (PerFn (MeasureFn 0.04 (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(documentation AbrinToxin "An extremely toxic protein found in the seeds of the rosary pea.  
Its mechanism, symptoms, and treatment are very similar to those of &%RicinToxin.  However, 
&%AbrinToxin is roughly 75 times more toxic than &%RicinToxin.")

(subclass ChemicalAgent (ComplementFn OrganicObject))

(=>
   (instance ?WEAPON ChemicalWeapon)
   (exists (?AGENT)
      (and
         (instance ?AGENT ChemicalAgent)
         (part ?AGENT ?WEAPON))))

(=>
   (instance ?AGENT ChemicalAgent)
   (not (exists (?ORGANISM ?PROCESS ?RESULT)
      (and
         (instance ?ORGANISM Organism)
         (instance ?PROCESS BiologicalProcess)
         (instrument ?PROCESS ?ORGANISM)
         (result ?PROCESS ?SUBSTANCE)
         (copy ?SUBSTANCE ?AGENT)))))

(subclass BlisterAgent ChemicalAgent)
(biochemicalAgentDelivery BlisterAgent Touching)
(biochemicalAgentDelivery BlisterAgent Breathing)
(biochemicalAgentDelivery BlisterAgent Ingesting)
(documentation BlisterAgent "&%ChemicalAgents that affect eyes, lungs, and skin.  
&%BlisterAgents are so named because they cause blistering of the skin.  They may 
also cause blindness and respiratory damage.") 

(=>
   (instance ?AGENT BlisterAgent)
   (hasPurpose ?AGENT
      (exists (?DAMAGE ?TISSUE)
         (and
            (instance ?DAMAGE Damaging)
            (instance ?TISSUE Tissue)
            (patient ?DAMAGE ?TISSUE)))))

(subclass MustardGas BlisterAgent)
(documentation MustardGas "A &%BlisterAgent that was commonly used in World War I 
to incapacitate troops.  It is chemically stable, persistent, and is capable of 
attacking the eyes, the skin, and the respiratory tract.  There is no accepted 
treatment or preventive measure for &%MustardGas.") 

(subclass SulphurMustardGas MustardGas)
(documentation SulphurMustardGas "A kind of mustard gas.  Also known as Yperite 
and HD.  This is regarded b some as the most effective &%BlisterAgent because it 
is persistent.")

(subclass NitrogenMustardGas MustardGas)
(documentation NitrogenMustardGas "A kind of mustard gas.  Also known as HN.")

(subclass Lewisite BlisterAgent)
(biochemicalAgentAntidote Lewisite Dimercaprol Covering)
(documentation Lewisite "Besides being a &%BlisterAgent, &%Lewisite causes 
systemic poisoning.  Its symptoms include pulmonary edema, diarrhea, reduced 
body temperature and blood pressure.  When large quantities of &%Lewisite are 
inhaled, it can cause death in as little as 10 minutes.")

(subclass PhosgeneOxime BlisterAgent)
(documentation PhosgeneOxime "One of the most painful and destructive chemical 
compounds in existence.  Recovery from this &%ChemicalAgent requires one to 
three months.") 

(subclass BloodAgent ChemicalAgent)
(biochemicalAgentDelivery BloodAgent Breathing)
(documentation BloodAgent "A &%CompoundSubstance that prevents the normal use 
of oxygen by bodily tissues.  &%BloodAgents are highly volatile and disperse 
easily in the open air.")

(subclass HydrogenCyanide BloodAgent)
(documentation HydrogenCyanide "A &%ChemicalAgent that was widely used by the 
Nazis in extermination camps.  A low dose can cause headache, disorientation, 
and vomiting, while a high dose can rapidly result in respiratory and/or cardiac 
arrest.")

(subclass CyanogenChloride BloodAgent)
(documentation CyanogenChloride "Used in mining and metalurgy, it is very 
similar to &%HydrogenCyanide.  However, it is less volatile than &%HydrogenCyanide, 
and its effects on the eyes, respiratory tract, and lungs is similar to &%Phosgene 
and &%ChorineGas.")

(subclass Arsine BloodAgent)
(documentation Arsine "A very toxic form of arsenic that is 2 and a half times 
denser than air.  Victims who die after exposure to &%Arsine generally die from 
renal failure.") 

(subclass IncapacitatingAgent ChemicalAgent)
(documentation IncapacitatingAgent "&%ChemicalAgents that are designed to cause 
temporary disability in victims.")

(subclass CSGas IncapacitatingAgent)
(documentation CSGas "Known as tear or pepper gas, often used by paramilitary 
and police to control unruly crowds, the gas can cause temporary blindness and 
tearing.")

(subclass BZGas IncapacitatingAgent)
(documentation BZGas "Stockpiled for a time by both NATO and the Warsaw Pact 
countries, &%BZGas is a psychoactive agent that is heaver than air and reputedly 
ten times more powerful than LSD.  Effects of the gas are not well understood, 
but they include vomiting, lethargy, loss of motor co-ordination, and cognitive 
incapacity.") 

(subclass NerveAgent ChemicalAgent)
(biochemicalAgentDelivery NerveAgent Breathing)
(biochemicalAgentSyndrome NerveAgent Paralysis)
(biochemicalAgentAntidote NerveAgent Atropine Poking)
(documentation NerveAgent "These &%ChemicalAgents are easily absorbed into 
the mucous membranes of humans and inactivate the essential enzyme 
acetylcholinesterase, bringing about paralysis of the respiratory and central 
nervous systems.") 

(subclass GSeriesNerveAgent NerveAgent)
(disjoint GSeriesNerveAgent VSeriesNerveAgent)
(documentation GSeriesNerveAgent "Earliest sort of &%NerveAgent (developed 
in the 1930's).  This &%subclass of &%NerveAgents tends to be less 
persistent, more volatile, and less toxic than &%VSeriesNerveAgents, 
which were developed later.  &%GSeriesNerveAgents include &%Tabun, 
&%Sarin, and &%Soman.") 

(subclass Tabun GSeriesNerveAgent)
(documentation Tabun "Also known as GA, one of the G-series nerve agents.  It 
is about half as toxic as &%Sarin, but it is more irritating to the eyes than 
&%Sarin.")

(subclass Soman GSeriesNerveAgent)
(biochemicalAgentAntidote Soman PyridostigmineBromide Putting)
(lethalDose Soman (PerFn (MeasureFn 64.0 (MicroFn Gram)) (MeasureFn 1.0 (KiloFn Gram))))
(documentation Soman "Also known as GD, one of the G-series nerve agents.")

(subclass Sarin GSeriesNerveAgent)
(lethalDose Sarin (PerFn (MeasureFn 100.0 (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(documentation Sarin "Also known as GB, one of the G-series nerve agents.")

(subclass GF GSeriesNerveAgent)
(documentation GF "One of the G-series nerve agents.") 

(subclass VSeriesNerveAgent NerveAgent)
(documentation VSeriesNerveAgent "More advanced sort of &%NerveAgent 
(developed in the 1950's).  This &%subclass of &%NerveAgents tends to be 
more persistent and toxic than &%GSeriesNerveAgents, which were developed 
earlier.  &%VSeriesNerveAgents include VE, VG, VM, VS, and VX.") 

(subclass VX VSeriesNerveAgent)
(lethalDose VX (PerFn (MeasureFn 15.0 (MicroFn Gram)) (MeasureFn 1.0 (KiloFn Gram))))
(documentation VX "One of the V-series nerve agents.")

(subclass ChokingAgent ChemicalAgent)
(biochemicalAgentDelivery ChokingAgent Breathing)
(documentation ChokingAgent "&%ChemicalAgents designed to cause the lungs to fill 
with fluid, which can result in the victim choking to death.  &%ChokingAgents are 
heavy gases that are less effective than other &%ChemicalAgents because they can be 
easily dissipated by a slight wind.") 

(subclass Diphosgene ChokingAgent)
(documentation Diphosgene "A poisonous gas that irritates the lungs and that is 
similar to &%Phosgene.")

(subclass Chloropicrin ChokingAgent)
(biochemicalAgentSyndrome Chloropicrin Gastroenteritis)
(documentation Chloropicrin "Poisonous gas which has effects similar to 
tear gas.  If ingested, the gas causes gastroenteritis.") 

(subclass Phosgene ChokingAgent)
(documentation Phosgene "A colorless, extremely toxic gas.  It is regarded as the 
most dangerous of the &%ChemicalAgents.  Deaths resulting from exposure to this 
&%ChemicalAgent generally occur in 24 to 48 hours.")

(subclass ChlorineGas ChokingAgent)
(documentation ChlorineGas "A poisonous gas.")

(subclass WMDWeaponsProductionFacility StationaryArtifact)
(documentation WMDWeaponsProductionFacility "Facilities where instances of 
&%WeaponOfMassDestruction are built.")

(=>
   (instance ?FACILITY WMDWeaponsProductionFacility)
   (exists (?DEVELOP)
      (and
         (instance ?DEVELOP DevelopingWeaponOfMassDestruction)
         (located ?DEVELOP ?FACILITY))))

(subclass WMDWeaponsResearchFacility StationaryArtifact)
(documentation WMDWeaponsResearchFacility "Facilities where research on 
instances of &%WeaponOfMassDestruction is performed.") 

(=>
   (instance ?FACILITY WMDWeaponsResearchFacility)
   (exists (?RESEARCH)
      (and
         (instance ?RESEARCH ResearchingWeaponOfMassDestruction)
         (located ?RESEARCH ?FACILITY))))

(partition RadioactiveWeapon NuclearWeapon RadiologicalWeapon)

(subclass NuclearWeaponProductionFacility WMDWeaponsProductionFacility)
(documentation NuclearWeaponProductionFacility "Facilities where &%NuclearWeapons 
are built.")

(=>
   (and
      (instance ?FACILITY NuclearWeaponProductionFacility)
      (instance ?DEVELOP DevelopingWeaponOfMassDestruction)
      (located ?DEVELOP ?FACILITY)
      (result ?DEVELOP ?WEAPON))
   (instance ?WEAPON NuclearWeapon))

(documentation NuclearWeaponResearchFacility "Research facilities that perform 
research on technology related to &%NuclearWeapons.")
(subclass NuclearWeaponResearchFacility WMDWeaponsResearchFacility)

(=>
   (and
      (instance ?FACILITY NuclearWeaponResearchFacility)
      (instance ?RESEARCH ResearchingWeaponOfMassDestruction)
      (located ?RESEARCH ?FACILITY))
   (refers ?RESEARCH NuclearWeapon))

(partition WeaponOfMassDestruction RadioactiveWeapon BiochemicalWeapon)

(subclass DeployingWeaponOfMassDestruction IntentionalProcess)
(subclass DeployingWeaponOfMassDestruction Putting)
(documentation DeployingWeaponOfMassDestruction "Positioning a chemical, biological
or radioactive weapon for the purpose of bringing about harm of some kind.") 

(=>
   (and
      (instance ?DEPLOY DeployingWeaponOfMassDestruction)
      (patient ?DEPLOY ?WEAPON))
   (instance ?WEAPON WeaponOfMassDestruction))

(subclass DevelopingWeaponOfMassDestruction Making)
(documentation DevelopingWeaponOfMassDestruction "&%Making instances of 
&%WeaponOfMassDestruction.")

(=>
   (and
      (instance ?DEVELOP DevelopingWeaponOfMassDestruction)
      (result ?DEVELOP ?WEAPON))
   (instance ?WEAPON WeaponOfMassDestruction))

(subclass StockpilingWeaponOfMassDestruction Manufacture)
(subclass StockpilingWeaponOfMassDestruction DevelopingWeaponOfMassDestruction)
(documentation StockpilingWeaponOfMassDestruction "Manufacturing a significant quantity 
of instances of a &%WeaponOfMassDestruction, i.e. the weapons are not produced at the 
scale of a single laboratory or a pilot program.  These weapons may or may not be deployed 
(see &%DeployingWeaponOfMassDestruction).")

(subclass DeliveringWeaponOfMassDestruction Making)
(documentation DeliveringWeaponOfMassDestruction "Integrating a &%WeaponOfMassDestruction 
with a conventional weapon, so that the former can be delivered to its target.")

(=>
   (instance ?DELIVER DeliveringWeaponOfMassDestruction)
   (exists (?PUTTING ?WEAPON1 ?WEAPON2)
      (and
         (instance ?PUTTING Putting)
         (instance ?WEAPON1 WeaponOfMassDestruction)
         (instance ?WEAPON2 Weapon)
         (not (instance ?WEAPON2 WeaponOfMassDestruction))
         (patient ?PUTTING ?WEAPON1)
         (destination ?PUTTING ?WEAPON2))))


(subclass ResearchingWeaponOfMassDestruction Investigating)
(documentation ResearchingWeaponOfMassDestruction "Conducting research on the development 
of Weapons of Mass Destruction.")

(=>
   (instance ?RESEARCH ResearchingWeaponOfMassDestruction)
   (exists (?WEAPON)
      (and
         (subclass ?WEAPON WeaponOfMassDestruction)
         (refers ?RESEARCH ?WEAPON))))

(subclass DismantlingWeaponOfMassDestruction IntentionalProcess)
(documentation DismantlingWeaponOfMassDestruction "Dismantling a Weapon of Mass 
Destruction, i.e. destroying the weapon or removing it from active deployment.")

(=>
   (and
      (instance ?DISMANTLE DismantlingWeaponOfMassDestruction)
      (patient ?DISMANTLE ?WEAPON))
   (instance ?WEAPON WeaponOfMassDestruction))

(=>
   (instance ?DISMANTLE DismantlingWeaponOfMassDestruction)
   (exists (?PROCESS)
      (and
         (subProcess ?PROCESS ?DISMANTLE)
         (or 
            (instance ?PROCESS Destruction)
            (instance ?PROCESS Removing)))))

(=>
   (and
      (instance ?DISMANTLE DismantlingWeaponOfMassDestruction)
      (patient ?DISMANTLE ?WEAPON))
   (exists (?DEVELOP)
      (and
         (instance ?DEVELOP DevelopingWeaponOfMassDestruction)
         (result ?DEVELOP ?WEAPON)
         (earlier (WhenFn ?DEVELOP) (WhenFn ?DISMANTLE)))))

(instance biochemicalAgentSyndrome BinaryPredicate)
(domainSubclass biochemicalAgentSyndrome 1 BiochemicalAgent)
(domain biochemicalAgentSyndrome 2 DiseaseOrSyndrome)
(documentation biochemicalAgentSyndrome "Relates a &%subclass of &%BiochemicalAgent 
to a &%DiseaseOrSyndrome that is caused by or often associated with the 
&%BiochemicalAgent.")

(increasesLikelihood
   (and
      (biochemicalAgentSyndrome ?AGENT ?SYMPTOM)
      (biochemicalAgentDelivery ?AGENT ?PROCESSTYPE)
      (instance ?PROCESS ?PROCESSTYPE)
      (experiencer ?PROCESS ?ORGANISM))
   (attribute ?ORGANISM ?SYMPTOM))

(=>
   (and
      (biochemicalAgentSyndrome ?AGENT ?SYNDROME)
      (diseaseSymptom ?SYNDROME ?SYMPTOM))
   (biochemicalAgentSyndrome ?AGENT ?SYMPTOM))

(instance biochemicalAgentDelivery BinaryPredicate)
(domainSubclass biochemicalAgentDelivery 1 BiochemicalAgent)
(domainSubclass biochemicalAgentDelivery 2 Process)
(documentation biochemicalAgentDelivery "(&%biochemicalAgentDelivery ?AGENT ?PROCESS) 
means that the &%Process ?PROCESS is capable of infecting an organism with the 
&%BiochemicalAgent ?AGENT when the organism is the &%experiencer and the ?AGENT the 
&%patient of an instance of ?PROCESS.")

(=>
   (and
      (biochemicalAgentDelivery ?AGENT ?PROCESS)
      (subclass ?SUB ?PROCESS))
   (biochemicalAgentDelivery ?AGENT ?SUB))

(instance effectiveDose BinaryPredicate)
(domainSubclass effectiveDose 1 BiochemicalAgent)
(domain effectiveDose 2 FunctionQuantity)
(documentation effectiveDose "(&%effectiveDose ?AGENT ?QUANTITY) means that 
?QUANTITY is the effective dose, or ED50, for the &%BiochemicalAgent ?AGENT.  
This is the dose that would incapacitate 50% of the exposed human population.  
Note that ?QUANTITY is generally expressed in micrograms per kilogram (mcg/kg).")

(=>
   (effectiveDose ?AGENT (PerFn (MeasureFn ?NUMBER1 ?UNIT1) (MeasureFn ?NUMBER2 ?UNIT2)))
   (and
      (instance ?UNIT1 MassMeasure)
      (instance ?UNIT2 MassMeasure))) 
   
(instance lethalDose BinaryPredicate)
(domainSubclass lethalDose 1 BiochemicalAgent)
(domain lethalDose 2 FunctionQuantity)
(relatedInternalConcept lethalDose effectiveDose)
(documentation lethalDose "(&%lethalDose ?AGENT ?QUANTITY) means that ?QUANTITY 
is the lethal dose, or LD50, for the &%BiochemicalAgent ?AGENT.  This is the dose 
that would result in death for 50% of the exposed human population.  Note that 
?QUANTITY is generally expressed in micrograms per kilogram (mcg/kg).")

(=>
   (and
      (effectiveDose ?AGENT ?NUMBER1)
      (lethalDose ?AGENT ?NUMBER2))
   (greaterThan ?NUMBER2 ?NUMBER1))

(=>
   (lethalDose ?AGENT (PerFn (MeasureFn ?NUMBER1 ?UNIT1) (MeasureFn ?NUMBER2 ?UNIT2)))
   (and
      (instance ?UNIT1 MassMeasure)
      (instance ?UNIT2 MassMeasure)))

(instance diseaseSymptom BinaryPredicate)
(domain diseaseSymptom 1 DiseaseOrSyndrome)
(domain diseaseSymptom 2 DiseaseOrSyndrome)
(documentation diseaseSymptom "(&%diseaseSymptom ?DISEASE ?SYMPTOM) means that 
&%DiseaseOrSyndrome ?DISEASE is often associated with the &%DiseaseOrSyndrome 
?SYMPTOM, i.e. an &%Organism which suffers from ?DISEASE is more likely to 
suffer from ?SYMPTOM than one which does not.")

(=>
   (diseaseSymptom ?DISEASE ?SYMPTOM)
   (increasesLikelihood 
      (attribute ?ORGANISM ?DISEASE)
      (attribute ?ORGANISM ?SYMPTOM)))

(instance diseaseMortality BinaryPredicate)
(domain diseaseMortality 1 DiseaseOrSyndrome)
(domain diseaseMortality 2 RealNumber)
(documentation diseaseMortality "(&%diseaseMortality ?DISEASE ?NUMBER) means 
that &%DiseaseOrSyndrome ?DISEASE has a Mortality rate of ?NUMBER.")

(=>
   (diseaseMortality ?DISEASE ?RATE)
   (conditionalProbability
      (attribute ?ORGANISM ?DISEASE)
      (exists (?DEATH) (and (instance ?DEATH Death) (experiencer ?DEATH ?ORGANISM)))
      ?RATE))

(=>
   (diseaseMortality ?DISEASE ?RATE)
   (and
      (greaterThan ?RATE 0)
      (lessThan ?RATE 1)))

(=>
   (diseaseMortality ?DISEASE ?RATE)
   (instance ?DISEASE LifeThreateningDisease))

(instance biochemicalAgentAntidote TernaryPredicate)
(domain biochemicalAgentAntidote 1 BiochemicalAgent)
(domainSubclass biochemicalAgentAntidote 2 BiologicallyActiveSubstance)
(domainSubclass biochemicalAgentAntidote 3 Process)
(documentation biochemicalAgentAntidote "(&%biochemicalAgentAntidote ?AGENT ?SUBSTANCE ?PROCESS) 
means that the &%BiologicallyActiveSubstance ?SUBSTANCE has been shown to be effective in 
treating someone who has been exposed to the &%BiochemicalAgent ?AGENT when the ?SUBSTANCE is 
administered via the &%Process ?PROCESS.")

(decreasesLikelihood
   (and
      (biochemicalAgentSyndrome ?AGENT ?SYMPTOM)
      (biochemicalAgentAntidote ?AGENT ?SUBSTANCE ?PROCESS)
      (instance ?SAMPLE ?SUBSTANCE)
      (instance ?THERAPY ?PROCESS)
      (experiencer ?THERAPY ?ORGANISM)
      (patient ?THERAPY ?SUBSTANCE))
   (attribute ?ORGANISM ?SYMPTOM))

(instance diseaseTreatment TernaryPredicate)
(domain diseaseTreatment 1 DiseaseOrSyndrome)
(domainSubclass diseaseTreatment 2 BiologicallyActiveSubstance)
(domainSubclass diseaseTreatment 3 Process)
(documentation diseaseTreatment "(&%diseaseTreatment ?DISEASE ?SUBSTANCE ?PROCESS) 
means that the &%BiologicallyActiveSubstance ?SUBSTANCE is effective in the treatment 
of the &%DiseaseOrSyndrome ?DISEASE when administered via the &%Process ?PROCESS, i.e. 
it has been demonstrated (in a significant sample of patients) to cure the ?DISEASE or 
at least reduce the severity of symptoms associated with the ?DISEASE.")

(=>
   (diseaseTreatment ?DISEASE ?SUBSTANCE ?PROCESS)
   (=>
      (holdsDuring ?TIME (attribute ?ORGANISM ?DISEASE))
      (increasesLikelihood
         (exists (?SAMPLE)
            (and
               (temporalPart (WhenFn ?PROCESS) ?TIME)
               (experiencer ?PROCESS ?ORGANISM)
               (patient ?PROCESS ?SAMPLE)
               (instance ?SAMPLE ?SUBSTANCE)))
         (holdsDuring (ImmediateFutureFn ?TIME) (not (attribute ?ORGANISM ?DISEASE))))))

(=>
   (and
      (diseaseTreatment ?DISEASE ?SUBSTANCE ?PROCESS)
      (subclass ?SUB ?PROCESS))
   (diseaseTreatment ?DISEASE ?SUBSTANCE ?SUB))

(=>
   (and
      (diseaseTreatment ?DISEASE ?SUBSTANCE ?PROCESS)
      (subclass ?SUB ?SUBSTANCE))
   (diseaseTreatment ?DISEASE ?SUB ?PROCESS))

(instance biologicalAgentCarrier BinaryPredicate)
(domainSubclass biologicalAgentCarrier 1 BiologicalAgent)
(domainSubclass biologicalAgentCarrier 2 Organism)
(documentation biologicalAgentCarrier "(&%biologicalAgentCarrier ?AGENT ?ORGANISM) 
means that the &%subclass of &%Organism ?ORGANISM is a carrier of the &%subclass of 
&%BiologicalAgent ?AGENT.") 

(=>
   (and
      (biologicalAgentCarrier ?AGENT ?ORGANISM)
      (subclass ?SUB ?ORGANISM))
   (biologicalAgentCarrier ?AGENT ?SUB))
   
(instance secretesToxin BinaryPredicate)
(domainSubclass secretesToxin 1 Organism)
(domainSubclass secretesToxin 2 Toxin)
(documentation secretesToxin "(&%secretesToxin ?ORGANISM ?TOXIN) means that the 
&%subclass of &%Organism ?ORGANISM produces the &%subclass of &%Toxin ?TOXIN.")

(=>
   (secretesToxin ?ORGANISM ?TOXIN)
   (forall (?INSTANCE1)
      (=>
         (instance ?INSTANCE1 ?ORGANISM)
         (exists (?PROCESS ?INSTANCE2)
            (and
               (instance ?PROCESS BiologicalProcess)
               (experiencer ?PROCESS ?INSTANCE1)
               (result ?PROCESS ?INSTANCE2)
               (instance ?INSTANCE2 ?TOXIN))))))

(subclass SalmonellaTyphimurium BacterialAgent)
(biochemicalAgentSyndrome SalmonellaTyphimurium Gastroenteritis)
(biochemicalAgentSyndrome SalmonellaTyphimurium Fever)
(documentation SalmonellaTyphimurium "A bacterium that can cause death in young, 
elderly or immunodeficient people.  It appears in the stool of infected people.")

(subclass SalmonellaPartyphi BacterialAgent)
(documentation SalmonellaPartyphi "Causes a less severe illness than &%SalmonellaTyphimurium.")

(subclass LifeThreateningAgent BiochemicalAgent)
(documentation LifeThreateningAgent "A &%BiochemicalAgent that has been observed 
to cause the death of &%Humans.")

(=>
   (instance ?AGENT LifeThreateningAgent)
   (capability Killing instrument ?AGENT))

(=>
   (and
      (lethalDose ?AGENT ?NUMBER)
      (greaterThan ?NUMBER 0))
   (instance ?AGENT LifeThreateningAgent))

(subclass LifeThreateningDisease DiseaseOrSyndrome)
(documentation LifeThreateningDisease "A &%DiseaseOrSyndrome that has been observed 
to cause the death of &%Humans.")

(=>
   (and
      (instance ?DISEASE LifeThreateningDisease)
      (attribute ?AGENT ?DISEASE))
   (capability Death experiencer ?AGENT))

(=>
   (and
      (instance ?DISEASE LifeThreateningDisease)
      (biochemicalAgentSyndrome ?AGENT ?DISEASE))
   (instance ?AGENT LifeThreateningAgent))

(subclass VaccinatableDisease InfectiousDisease)
(documentation VaccinatableDisease "A disease for which there is an effective 
vaccine for &%Humans.")

(=>
   (instance ?DISEASE VaccinatableDisease)
   (exists (?VACCINE)
      (and
         (instance ?VACCINE BiologicallyActiveSubstance)
         (=>
            (and
               (instance ?PUT Putting)
               (destination ?PUT ?HUMAN)
               (instance ?HUMAN Human))
            (holdsDuring (FutureFn (WhenFn ?PUT)) (not (attribute ?HUMAN ?DISEASE)))))))

(instance Gastroenteritis DiseaseOrSyndrome)
(documentation Gastroenteritis "Inflammation of the gastrointestinal tract, i.e. 
the stomach and small and large intestines.  The symptoms include diarrhea, vomiting, 
and abdominal pain.")

(=>
   (and
      (instance ?ORGANISM Human)
      (attribute ?ORGANISM Fever))
   (exists (?NUMBER)
      (and
         (measure ?ORGANISM (MeasureFn ?NUMBER FahrenheitDegree))
         (greaterThan ?NUMBER 98.6))))

(instance Paralysis DiseaseOrSyndrome)
(documentation Paralysis "The syndrome where some or all of an &%Animal's muscles 
cannot be moved voluntarily.")

(=>
   (attribute ?ORGANISM Paralysis)
   (exists (?MUSCLE)
      (and
         (instance ?MUSCLE Muscle)
         (part ?MUSCLE ?ORGANISM)
         (not (exists (?MOTION)
            (and
               (instance ?MOTION BodyMotion)
               (agent ?MOTION ?ORGANISM)
               (patient ?MOTION ?MUSCLE)))))))

(subclass HemorrhagicFever ViralDisease)
(documentation HemorrhagicFever "A severe syndrome that affects multiple organs of 
the body.  Typically, the vascular system is damaged, and the body's ability to regulate 
itself is impaired.")

(=>
   (and
      (instance ?BACTERIUM BacterialAgent)
      (biochemicalAgentSyndrome ?BACTERIUM ?SYNDROME))
   (instance ?SYNDROME BacterialDisease))

(=>
   (and
      (instance ?VIRUS ViralAgent)
      (biochemicalAgentSyndrome ?VIRUS ?SYNDROME))
   (instance ?SYNDROME ViralDisease))

(instance Anthrax BacterialDisease)
(diseaseTreatment Anthrax Doxycycline Ingesting)
(diseaseTreatment Anthrax Ciprofloxacin Ingesting)
(diseaseIncubation Anthrax (MeasureFn 1 HourDuration) (MeasureFn 2 HourDuration))
(documentation Anthrax "The disease caused by &%BacillusAnthracis.  Victims of 
the disease may experience a brief, apparent recovery before death.")

(subAttribute InhalationalAnthrax Anthrax)
(instance InhalationalAnthrax LifeThreateningDisease)
(documentation InhalationalAnthrax "Almost 100% fatal if left untreated for more 
than 24 hours.")

(=>
   (attribute ?ORGANISM InhalationalAnthrax)
   (exists (?ANTHRACIS ?BREATHING)
      (and
         (instance ?ANTHRACIS BacillusAnthracis)
         (instance ?BREATHING Breathing)
         (agent ?BREATHING ?ORGANISM)
         (patient ?BREATHING ?ANTHRACIS))))

(instance Gonorrhea BacterialDisease)
(documentation Gonorrhea "A disease caused by the bacterium &%NeisseriaGonorrhoeae.")

(instance QFever BacterialDisease)
(diseaseSymptom QFever Fever)
(diseaseMortality QFever 0.01)
(documentation QFever "A disease caused by the rickettsia &%CoxiellaBurnetii.  Causes 
headache, chills, coughing, hallucinations, fever up to 104 degrees, facial pain, speech 
impairment, heart inflammation and congestive heart failure.")

(instance YellowSkin ViralDisease)
(documentation YellowSkin "Is caused by liver failure, which results in an accumulation 
of bile.  It is caused by &%YellowFeverVirus.")

(subclass YellowFeverVirus ViralAgent)
(biochemicalAgentSyndrome YellowFeverVirus Fever)
(biochemicalAgentSyndrome YellowFeverVirus YellowSkin)
(documentation YellowFeverVirus "A &%Virus that can result in chills, fever, stomach 
bleeding, and &%YellowSkin.")

(=>
   (instance ?VIRUS YellowFeverVirus)
   (exists (?NUMBER)
      (and
         (width ?VIRUS (MeasureFn ?NUMBER Meter))
         (equal ?NUMBER 0.00000002))))

(subclass FootAndMouthVirus ViralAgent)
(documentation FootAndMouthVirus "A &%Virus that is smaller than &%YellowFeverVirus.")

(=>
   (and
      (instance ?VIRUS1 FootAndMouthVirus)
      (instance ?VIRUS2 YellowFeverVirus))
   (smaller ?VIRUS1 ?VIRUS2))

(subclass EbolaVirus ViralAgent)
(biochemicalAgentSyndrome EbolaVirus EbolaHemorrhagicFever)
(documentation EbolaVirus "A &%Virus that causes a form of hemorrhagic fever.")

(instance EbolaHemorrhagicFever HemorrhagicFever)
(documentation EbolaHemorrhagicFever "A very serious &%HemorrhagicFever, which often results 
in death.")

(subclass FrancisellaTularensis BacterialAgent)
(biologicalAgentCarrier FrancisellaTularensis Rodent)
(biologicalAgentCarrier FrancisellaTularensis Arachnid)
(biochemicalAgentSyndrome FrancisellaTularensis Tularemia) 
(biochemicalAgentDelivery FrancisellaTularensis Breathing)
(documentation FrancisellaTularensis "The &%Bacterium that causes the disease 
&%Tularemia.  Also known as Pasturella Tularensis.  The &%Bacterium has two strains, 
Jellison type A (F. tularensis biovar tularensis) and type B strains (F tularensis 
biovar palaearctica), and it is infectious with a dose as small as 50 cells per 
milliliter.")

(instance Tularemia BacterialDisease)
(diseaseSymptom Tularemia Fever)
(diseaseMortality Tularemia 0.05)
(documentation Tularemia "A disease associated with fever, chills, coughing and 
Tularemia skin lesions (ulcers up to an inch wide).")

(instance Smallpox ViralDisease)
(diseaseMortality Smallpox 0.33)
(documentation Smallpox "A highly contagious and dangerous disease.  It causes blood 
loss, cardiovascular collapse, secondary infections, skin pustules, and often leaves 
survivors scarred and blinded.  Approximately 500 million people died from &%Smallpox 
in the nineteenth century.  The disease can live on objects for several days, and it 
incubates for twelve days before showing symptoms.")

(instance Monkeypox ViralDisease)
(documentation Monkeypox "A viral disease that is very similar to &%Smallpox.  It is 
seen sporadically in parts of Africa.")

(subclass MonkeypoxVirus ViralAgent)
(biochemicalAgentSyndrome MonkeypoxVirus Monkeypox)
(biochemicalAgentDelivery MonkeypoxVirus Poking)
(biologicalAgentCarrier MonkeypoxVirus Mammal)
(documentation MonkeypoxVirus "The &%Virus that causes &%Monkeypox.")

(subclass NeisseriaGonorrhoeae BacterialAgent)
(biochemicalAgentSyndrome NeisseriaGonorrhoeae Gonorrhea)
(documentation NeisseriaGonorrhoeae "The &%Bacterium that causes &%Gonorrhea.")

(subclass CoxiellaBurnetii RickettsialAgent)
(biochemicalAgentSyndrome CoxiellaBurnetii QFever)
(biochemicalAgentDelivery CoxiellaBurnetii Breathing)
(biochemicalAgentDelivery CoxiellaBurnetii Ingesting)
(documentation CoxiellaBurnetii "The &%Bacterium that causes &%QFever.")

(subclass VariolaMajor ViralAgent)
(biochemicalAgentSyndrome VariolaMajor Smallpox)
(documentation VariolaMajor "A &%Virus that causes &%Smallpox.  Also known 
as the smallpox virus.") 

(subclass VariolaMinor ViralAgent)
(biochemicalAgentSyndrome VariolaMinor Smallpox)
(documentation VariolaMinor "A &%Virus that causes &%Smallpox.  Also known 
as Alastrim.")

(=>
   (instance ?ANTIBIOTIC Antibiotic)
   (capability (KappaFn ?KILL (and 
                                 (instance ?KILL Killing) 
                                 (patient ?KILL ?BACTERIUM) 
                                 (instance ?BACTERIUM Bacterium))) 
                                          agent ?ANTIBIOTIC))

(=>
   (diseaseTreatment ?DISEASE Antibiotic ?PROCESS)
   (instance ?DISEASE BacterialDisease))

(=>
   (instance ?DISEASE ViralDisease)
   (exists (?PROCESS)
      (not (diseaseTreatment ?DISEASE Antibiotic ?PROCESS))))

(subclass OralAntibiotic Antibiotic)
(documentation OralAntibiotic "An &%Antibiotic that is administered orally, 
e.g. &%Penicillin.")

(=>
   (diseaseTreatment ?DISEASE OralAntibiotic ?PROCESS)
   (subclass ?PROCESS Ingesting))  

(subclass Penicillin OralAntibiotic)
(documentation Penicillin "An ingestible antibiotic which kills many kinds of 
bacteria.")

(subclass Doxycycline OralAntibiotic)
(documentation Doxycycline "An &%OralAntibiotic that is used to treat a wide variety 
of &%BacterialDiseases, including traveler's diarrhea.  Recommended for prevention of 
inhalational &%Anthrax in subjects who may have been exposed to aeresolized 
&%BacillusAnthracis.")

(subclass Ciprofloxacin OralAntibiotic)
(documentation Ciprofloxacin "An &%OralAntibiotic that is often used to prevent 
complications from &%AquiredImmunoDeficiencySyndrome.  Recommended for prevention 
of inhalational &%Anthrax in subjects who may have been exposed to aeresolized 
&%BacillusAnthracis.")  

(subclass Keflex OralAntibiotic)
(documentation Keflex "A cephalosporin for treating bacterial infections.")

(subclass TopicalAntibiotic Antibiotic)
(documentation TopicalAntibiotic "An &%Antibiotic that is administered topically, 
e.g. &%Bacitracin.")

(=>
   (diseaseTreatment ?DISEASE TopicalAntibiotic ?PROCESS)
   (subclass ?PROCESS Covering))

(subclass Bacitracin TopicalAntibiotic)
(documentation Bacitracin "A common topical antibiotic.")

(=>
   (diseaseTreatment ?DISEASE Bacitracin ?PROCESS)
   (and
      (instance ?DISEASE BacterialDisease)
      (instance ?PROCESS Putting))) 

(subclass Atropine BiologicallyActiveSubstance)
(documentation Atropine "An anticholinergic agent.")

(subclass ShigellaDysenteriae BacterialAgent)
(secretesToxin ShigellaDysenteriae ShigaToxin)
(biochemicalAgentDelivery ShigellaDysenteriae Ingesting)
(documentation ShigellaDysenteriae "Causes severe dysentery (even when as little as 
100 bacteria are ingested).  This form of dysentery results in death in 10-20 percent 
of infections.") 

(subclass ShigaToxin Toxin)
(lethalDose ShigaToxin (PerFn (MeasureFn 0.002 (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(documentation ShigaToxin "The &%Toxin produced by the &%Bacterium &%ShigellaDysenteriae.")

(subclass AquiredImmunoDeficiencySyndrome DiseaseOrSyndrome)
(documentation AquiredImmunoDeficiencySyndrome "A disease that attacks the immune 
system and that is caused by the &%HIVVirus.  Patients are usually infected through 
blood or semen.")

(subclass HIVVirus ViralAgent)
(biochemicalAgentDelivery HIVVirus SexualReproduction)
(biochemicalAgentSyndrome HIVVirus AquiredImmunoDeficiencySyndrome)
(documentation HIVVirus "The &%Virus that causes &%AquiredImmunoDeficiencySyndrome.")

(subclass SalmonellaTyphi BacterialAgent)
(biochemicalAgentDelivery SalmonellaTyphi Ingesting) 
(biochemicalAgentSyndrome SalmonellaTyphi TyphoidFever)
(documentation SalmonellaTyphi "The &%Bacterium that causes &%TyphoidFever.")

(subclass TyphoidFever DiseaseOrSyndrome)
(diseaseSymptom TyphoidFever Fever)
(diseaseTreatment TyphoidFever Antibiotic Ingesting)
(documentation TyphoidFever "A life-threatening disease that is caused by 
&%SalmonellaTyphi.")

(subclass GiardiaLamblia BacterialAgent)
(biochemicalAgentSyndrome GiardiaLamblia Gastroenteritis)
(biologicalAgentCarrier GiardiaLamblia Rodent)
(documentation GiardiaLamblia "A &%Bacterium that causes &%Gastroenteritis.  It is 
present in many streams and lakes, and it is carried by beavers and other animals.")

(subclass SerratiaMarcenscens BacterialAgent)
(subclass SerratiaMarcenscens LifeThreateningAgent)
(documentation SerratiaMarcenscens "A &%Bacterium that can cause death in infected 
&%Humans.")

(subclass CoccidioidesImmitis FungalAgent)
(subclass CoccidioidesImmitis LifeThreateningAgent)
(biochemicalAgentSyndrome CoccidioidesImmitis RiftValleyFever)
(documentation CoccidioidesImmitis "A &%Fungus that causes &%RiftValleyFever, 
an illness whose symptoms include fever, chills, and coughing and which may 
result in death.")

(subclass CoccidiodesPosadasii FungalAgent)
(subclass CoccidiodesPosadasii LifeThreateningAgent)
(biochemicalAgentSyndrome CoccidiodesPosadasii RiftValleyFever)
(documentation CoccidiodesPosadasii "A recently discovered &%Fungus that is related to 
&%CoccidioidesImmitis and that also causes &%RiftValleyFever.")

(subclass Mixomatosis ViralAgent)
(relatedInternalConcept Mixomatosis VariolaMajor) 
(biologicalAgentCarrier Mixomatosis Rodent)
(documentation Mixomatosis "A &%Virus that is infectious to rabbits and may 
cause blindness and death in these &%Rodents.")

(subclass VenezuelanEquineEncephalitis ViralDisease)
(subclass VenezuelanEquineEncephalitis VaccinatableDisease)
(diseaseMortality VenezuelanEquineEncephalitis 0.01)
(diseaseSymptom VenezuelanEquineEncephalitis Fever)
(documentation  VenezuelanEquineEncephalitis "Also known as Venezuelan equine 
encephalomyelitis.  Caused by any one of eight distinct &%Viruses.  Symptoms of 
the disease include severe headache, high fever (up to 105 degrees), sensitivity 
to light (photophobia), nausea, coughing, and diarrhea.  The disease results in 
central nervous system infection in 1 of 25 children.") 

(subclass VenezuelanEquineEncephalitisVirus ViralAgent)
(biochemicalAgentSyndrome VenezuelanEquineEncephalitisVirus VenezuelanEquineEncephalitis)
(documentation VenezuelanEquineEncephalitisVirus "The &%Virus that causes 
&%VenezuelanEquineEncephalitis.")   

(subclass RiftValleyFever ViralDisease)
(diseaseSymptom RiftValleyFever Fever)
(diseaseMortality RiftValleyFever 0.01)
(documentation RiftValleyFever "A &%ViralDisease that causes chills, bleeding, 
and stupor.")

(subclass StaphyylococcusAureus BacterialAgent)
(secretesToxin StaphyylococcusAureus StaphylococcalEnterotoxinB)
(biochemicalAgentSyndrome StaphyylococcusAureus Fever)
(documentation StaphyylococcusAureus "A bacterium that secretes &%StaphylococcalEnterotoxicB.  
It causes chills, headache, muscle pain, coughing (which may last for weeks), and sudden 
fever of up to 106 degrees (which may last for days).  It occasionally causes nausea, 
vomiting, and diarrhea.")

(subclass StaphylococcalEnterotoxinB Toxin)
(subclass StaphylococcalEnterotoxinB LifeThreateningAgent)
(effectiveDose StaphylococcalEnterotoxinB (PerFn 0.0004 (MeasureFn (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(lethalDose StaphylococcalEnterotoxinB (PerFn (MeasureFn 0.02 (MicroFn Gram)) (MeasureFn 1 (KiloFn Gram)))) 
(biochemicalAgentDelivery StaphylococcalEnterotoxinB Ingesting) 
(biochemicalAgentDelivery StaphylococcalEnterotoxinB Breathing) 
(documentation StaphylococcalEnterotoxinB "A common cause of food poisoning.  It has 
been studied as a &%BiologicalAgent, because it is stable, can be converted to aerosol 
form, and can be lethal when inhaled.")

(subclass Influenza ViralDisease)
(subclass Influenza VaccinatableDisease)
(diseaseSymptom Influenza Fever)
(documentation Influenza "A contagious disease caused by a large number of &%Viruses.")

(subclass Saxitoxin Toxin)
(subclass Saxitoxin CompoundSubstance)
(subclass Saxitoxin BodySubstance)
(biologicalAgentCarrier Saxitoxin Mollusk)
(biochemicalAgentDelivery Saxitoxin Ingesting)
(biochemicalAgentDelivery Saxitoxin Breathing)
(biochemicalAgentSyndrome Saxitoxin ParalyticShellfishPoisoning)
(documentation Saxitoxin "A class of chemically related neurotoxins that are produced 
by marine dinoflagellates and carried by &%Mollusks.")

(instance ParalyticShellfishPoisoning LifeThreateningDisease)
(diseaseSymptom ParalyticShellfishPoisoning Paralysis)
(documentation ParalyticShellfishPoisoning "A very serious disease requiring immediate 
medical attention.  There is no vaccine for this disease.")

(subclass Conotoxin Toxin)
(subclass Conotoxin CompoundSubstance)
(subclass Conotoxin BodySubstance)
(biochemicalAgentCarrier Conotoxin Mollusk)
(biochemicalAgentSyndrome Conotoxin ParalyticShellfishPoisoning)
(documentation Conotoxin "A class of neurotoxins that are produced by the Pacific cone 
snails.  The lethality of these toxins varies widely, and it would be impractical to 
manufacture them on a large scale.")

(subclass Tetrodotoxin Toxin)
(subclass Tetrodotoxin CompoundSubstance)
(subclass Tetrodotoxin BodySubstance)
(biochemicalAgentSyndrome Tetrodotoxin ParalyticShellfishPoisoning)
(documentation Tetrodotoxin "A &%Toxin produced by the pufferfish and several other (widely varying) 
species.  The initial symptoms include numbness in the lips and tongue, which spreads 
throughout the body.  The next phase of the poisoning is paralysis, which again spreads 
throughout the body.") 

(instance Cholera BacterialDisease)
(instance Cholera VaccinatableDisease)
(instance Cholera LifeThreateningDisease)
(documentation Cholera "A diarrheal disease that is caused by a &%Toxin secreted by the 
&%Bacterium &%VibrioCholera.  Death may result from severe dehydration caused by the 
disease.  There is little if any fever and abdominal pain with this illness.") 

(subclass VibrioCholera BacterialAgent)
(biochemicalAgentDelivery VibrioCholera Ingesting)
(biochemicalAgentSyndrome VibrioCholera Cholera)
(documentation VibrioCholera "The &%Bacterium that is responsible for &%Cholera.")

(instance Tetanus BacterialDisease)
(instance Tetanus VaccinatableDisease)
(diseaseIncubation Tetanus (MeasureFn 3 DayDuration) (MeasureFn 3 WeekDuration))
(documentation Tetanus "Also known as lockjaw, a &%BacterialDisease that affects 
the nervous system.  A subject is infected with the disease by a puncture of the 
skin that becomes infected with the &%ClostridiumTetani bacterium.  Symptoms of 
the disease include muscular stiffness (especially in the jaw and neck), difficulty 
swallowing, spasms, sweating, and fever.") 

(subclass ClostridiumTetani BacterialAgent) 
(biochemicalAgentDelivery ClostridiumTetani Poking)
(biochemicalAgentSyndrome ClostridiumTetani Tetanus)
(documentation ClostridiumTetani "The &%Bacterium that causes the disease &%Tetanus.")

(instance HerpesB ViralDisease)
(instance HerpesB LifeThreateningDisease)
(diseaseIncubation HerpesB (MeasureFn 10 DayDuration) (MeasureFn 20 DayDuration))
(diseaseMortality HerpesB 0.7)
(documentation HerpesB "The disease caused by &%HerpesBVirus.")

(subclass HerpesBVirus ViralAgent)
(biologicalAgentCarrier HerpesBVirus Monkey)
(documentation HerpesBVirus "A virus which is carried by Macaque monkeys and 
which does not harm these primates, but which causes the very serious &%HerpesB 
disease in humans.")

(instance diseaseIncubation TernaryPredicate)
(domain diseaseIncubation 1 DiseaseOrSyndrome)
(domainSubclass diseaseIncubation 2 TimeDuration)
(domainSubclass diseaseIncubation 3 TimeDuration)
(documentation diseaseIncubation "A predicate that specifies the time frame for the 
incubation of a &%DiseaseOrSyndrome.  (&%diseaseIncubation ?DISEASE ?TIME1 ?TIME2) means 
that the &%DiseaseOrSyndrome ?DISEASE will appear between ?TIME1 and ?TIME2 after the 
subject has been infected with the &%BiologicalAgent causing ?DISEASE.")

(instance sideEffect BinaryPredicate)
(domain sideEffect 1 BiologicallyActiveSubstance)
(domain sideEffect 2 DiseaseOrSyndrome)
(documentation sideEffect "A predicate that is used to specify a side effect of a 
substance used in a medical treatment.  (&%sideEffect ?SUBSTANCE ?SYNDROME) means 
that the &%BiologicallyActiveSubstance ?SUBSTANCE has the side effect ?SYNDROME.")

(instance Pertussis BacterialDisease)
(instance Pertussis VaccinatableDisease)
(diseaseTreatment Pertussis Antibiotic Ingesting)
(diseaseIncubation Pertussis (MeasureFn 6 DayDuration) (MeasureFn 21 DayDuration))
(documentation Pertussis "The &%BacterialDisease caused by &%BordetellaPertussis.  Its 
symptoms include severe coughing (hence the colloquial name of whooping cough) and 
flu-like symptoms.")

(subclass BordetellaPertussis BacterialAgent)
(biologicalAgentCarrier BordetellaPertussis Human)
(biochemicalAgentSyndrome BordetellaPertussis Pertussis)
(biochemicalAgentDelivery BordetellaPertussis Breathing)
(documentation BordetellaPertussis "The &%Bacterium that causes &%Pertussis.")

(subclass YersiniaPseudotuberculosis BacterialAgent)
(documentation YersiniaPseudotuberculosis "Causes a disease whose symptoms include 
diarrhea, fever, headache, skin ulcers, and post-infectious arthritis.")

(subclass ChlamydiaPsittaci BacterialAgent)
(biochemicalAgentSyndrome ChlamydiaPsittaci Psittacosis)
(biologicalAgentCarrier ChlamydiaPsittaci Bird)
(biochemicalAgentDelivery ChlamydiaPsittaci Breathing)
(documentation ChlamydiaPsittaci "The &%Bacterium that causes &%Psittacosis.")

(instance Psittacosis BacterialDisease)
(diseaseIncubation Psittacosis (MeasureFn 6 DayDuration) (MeasureFn 19 DayDuration))
(documentation Psittacosis "A serious disease caused by &%ChlamydiaPsittaci that 
affects both birds and humans.  In humans the symptoms include fever, chills, headache, 
muscle aches, and a dry cough.  The disease may give rise to pneumonia in extreme cases.")

(instance Malaria InfectiousDisease)
(instance Malaria LifeThreateningDisease)
(diseaseSymptom Malaria Fever)
(documentation Malaria "A disease that destroys red blood cells.  It is caused by a 
&%Microorganism (see &%MalarialPlasmodium) that is carried by the Anopheles Gambiae 
mosquito.")  

(subclass MalarialPlasmodium BiologicalAgent)
(subclass MalarialPlasmodium Microorganism)
(biologicalAgentCarrier MalarialPlasmodium Insect)
(biochemicalAgentDelivery MalarialPlasmodium Poking)
(documentation MalarialPlasmodium "Any of four strains of Plasmodium that are known 
to cause &%Malaria.")

(subclass PlasmodiumFalciparum MalarialPlasmodium)
(documentation PlasmodiumFalciparum "One of four strains of Plasmodium that are known to 
cause &%Malaria.  It can cause severe anemia and kidney failure or it can constrict small 
blood vessels and cause cerebral malaria.")

(subclass WestNileVirus ViralAgent)
(biologicalAgentCarrier WestNileVirus Insect)
(biochemicalAgentSyndrome WestNileVirus WestNileFever)
(biochemicalAgentDelivery WestNileVirus Poking)
(documentation WestNileVirus "A flavivirus that causes &%WestNileFever.")

(instance WestNileFever ViralDisease)
(documentation WestNileFever "A flu-like disease that is caused by the &%WestNileVirus.  
The disease is generally not life-threatening.  However, it can be very serious if it 
progresses to West Nile Encephalitis, West Nile Meningitis or West Nile Meningoencephalitis, 
all of which are inflammations of the brain and/or spinal cord.")

(subclass DengueFeverVirus ViralAgent)
(biologicalAgentCarrier DengueFeverVirus Insect)
(biochemicalAgentSyndrome DengueFeverVirus DengueFever)
(biochemicalAgentSyndrome DengueFeverVirus DengueHemorrhagicFever)
(biochemicalAgentDelivery DengueFeverVirus Poking)
(documentation DengueFeverVirus "Any of four related but distinct virus serotypes that are 
carried by the Aedes Aegypti mosquito and that can cause &%DengueFever and 
&%DengueHemorrhagicFever.")

(instance DengueFever ViralDisease)
(relatedInternalConcept DengueFever DengueHemorrhagicFever)
(documentation DengueFever "The less serious disease caused by the &%DengueFeverVirus.  
Symptoms include fever and severe headache.")

(instance DengueHemorrhagicFever HemorrhagicFever) 
(instance DengueHemorrhagicFever LifeThreateningDisease)
(diseaseMortality DengueHemorrhagicFever 0.05)
(documentation DengueHemorrhagicFever "The more serious disease caused by the &
%DengueFeverVirus.  Symptoms include bleeding from the nose, mouth, and gums, and 
excessive thirst and difficulty breathing.")

(subclass LaCrosseVirus ViralAgent)
(biologicalAgentCarrier LaCrosseVirus Insect)
(biologicalAgentCarrier LaCrosseVirus Rodent)
(biochemicalAgentDelivery LaCrosseVirus Poking)
(biochemicalAgentSyndrome LaCrosseVirus LaCrosseEncephalitis)
(documentation LaCrosseVirus "The &%Virus that causes &%LaCrossEncephalitis.  It is 
carried by the Aedes Triseriatus mosquito, as well as various woodlands rodents.")

(instance LaCrosseEncephalitis ViralDisease)
(diseaseMortality LaCrosseEncephalitis 0.01)
(documentation LaCrosseEncephalitis "Encephalitis caused by the &%LaCrosseVirus.  In its 
most severe cases, the disease may result in seizure and/or coma, but most infections are 
relatively mild.")

(subclass SaintLouisEncephalitisVirus ViralAgent)
(biologicalAgentCarrier SaintLouisEncephalitisVirus Bird)
(biologicalAgentCarrier SaintLouisEncephalitisVirus Insect)
(biochemicalAgentDelivery SaintLouisEncephalitisVirus Poking)
(biochemicalAgentSyndrome SaintLouisEncephalitisVirus SaintLouisEncephalitis)
(documentation SaintLouisEncephalitisVirus "The &%Virus that causes &%SaintLouisEncephalitis.  
Mosquitoes carry this virus, which is then transmitted to humans, when the insects bite them.")

(instance SaintLouisEncephalitis ViralDisease)
(instance SaintLouisEncephalitis LifeThreateningDisease)
(diseaseIncubation SaintLouisEncephalitis (MeasureFn 5 DayDuration) (MeasureFn 15 DayDuration))
(diseaseSymptom SaintLouisEncephalitis Fever)
(documentation SaintLouisEncephalitis "In mild cases, symptoms are flu-like.  In more 
severe cases, symptoms may include disorientation, coma, tremors, hemorrhage, and 
convulsions.")

(subclass EasternEquineEncephalitisVirus ViralAgent)
(biologicalAgentCarrier EasternEquineEncephalitisVirus Insect)
(biochemicalAgentDelivery EasternEquineEncephalitisVirus Poking)
(biochemicalAgentSyndrome EasternEquineEncephalitisVirus EasternEquineEncephalitis)
(documentation EasternEquineEncephalitisVirus "The &%Virus that causes 
&%EasternEquineEncephalitis.  Mosquitoes carry this virus, which is then transmitted 
to humans, when the insects bite them.")

(instance EasternEquineEncephalitis ViralDisease)
(instance EasternEquineEncephalitis LifeThreateningDisease)
(diseaseSymptom EasternEquineEncephalitis Fever)
(documentation EasternEquineEncephalitis "In mile cases, symptoms are flu-like.  In 
more severe cases, the disease may result in encephalitis, coma, and death.")

(subclass JapaneseEncephalitisVirus ViralAgent)
(biologicalAgentCarrier JapaneseEncephalitisVirus Insect)
(biochemicalAgentDelivery JapaneseEncephalitisVirus Poking)
(biochemicalAgentSyndrome JapaneseEncephalitisVirus JapaneseEncephalitis)
(documentation JapaneseEncephalitisVirus "The &%Virus that causes &%JapaneseEncephalitis.  
Mosquitoes in agricultural areas of Asia carry this virus, which is then transmitted to 
humans, when the insects bite them.")

(instance JapaneseEncephalitis ViralDisease)
(instance JapaneseEncephalitis LifeThreateningDisease)
(instance JapaneseEncephalitis VaccinatableDisease)
(diseaseIncubation JapaneseEncephalitis (MeasureFn 6 DayDuration) (MeasureFn 8 DayDuration))
(diseaseMortality JapaneseEncephalitis 0.30)
(documentation JapaneseEncephalitis "Most people who are infected with this disease 
will suffer only mild symptoms.  However, in severe cases, the disease attacks the 
central nervous system and may result in death.")

(subclass EscherichiaColi0157H7  BacterialAgent)
(biochemicalAgentDelivery EscherichiaColi0157H7 Ingesting) 
(documentation EscherichiaColi0157H7 "One of hundreds of strains of the Escherichia Coli 
&%Bacterium.  This strain lives in the intestines of healthy cattle, but it can cause 
severe illness when ingested by humans.")

(subclass MycobacteriumTuberculosis BacterialAgent)
(biochemicalAgentSyndrome MycobacteriumTuberculosis Tuberculosis)
(biochemicalAgentDelivery MycobacteriumTuberculosis Breathing)
(documentation MycobacteriumTuberculosis "The &%Bacterium that causes &%Tuberculosis.")

(instance Tuberculosis ViralDisease)
(instance Tuberculosis VaccinatableDisease)
(instance Tuberculosis LifeThreateningDisease)
(diseaseTreatment Tuberculosis OralAntibiotic Ingesting)
(documentation Tuberculosis "A disease that usually infects the lungs and causes severe 
coughing.  In some cases it will result in hemorrhage of the lungs.") 

(subclass MarburgVirus ViralAgent)
(biochemicalAgentDelivery MarburgVirus Breathing)
(biochemicalAgentDelivery MarburgVirus Poking)
(biochemicalAgentSyndrome MarburgVirus MarburgDisease)
(documentation MarburgVirus "The &%Virus that causes &%MarburgDisease.")

(instance MarburgDisease ViralDisease) 
(instance MarburgDisease LifeThreateningDisease)
(diseaseMortality MarburgDisease 0.25)
(diseaseIncubation MarburgDisease (MeasureFn 3 DayDuration) (MeasureFn 7 DayDuration))
(documentation MarburgDisease "Extremely serious disease caused by the &%MarburgVirus.  
Symptoms include high fever, myalgias, vomiting, and diarrhea.  Typically involves major 
organs, including the central nervous system.")  

(subclass StreptococcusA BacterialAgent)
(biochemicalAgentSyndrome StreptococcusA StrepThroat)
(biochemicalAgentSyndrome StreptococcusA ScarletFever)
(biochemicalAgentSyndrome StreptococcusA NecrotizingFaciitis)  
(documentation StreptococcusA "The &%Bacterium that causes strep throat, scarlet fever, 
and necrotizing faciitis (flesh-eating bacteria).")

(instance StrepThroat BacterialDisease)
(documentation StrepThroat "A &%BacterialDisease whose symptoms include a very sore 
throat, difficulty swallowing, high fever, white spots and/or pus on or near the tonsils, 
and swollen lymph nodes.  If left untreated, &%StrepThroat can progress to &%ScarletFever.")

(instance ScarletFever BacterialDisease)
(diseaseIncubation ScarletFever (MeasureFn 1 DayDuration) (MeasureFn 4 DayDuration))
(documentation ScarletFever "A serious disease that may arise if &%StrepThroat is left 
untreated.")

(instance NecrotizingFaciitis BacterialDisease) 
(instance NecrotizingFaciitis LifeThreateningDisease)
(documentation NecrotizingFaciitis "A &%BacterialDisease caused by &%StreptococcusA where 
the &%Bacterium rapidly attacks soft tissue.")  

(subclass HepatitisVirus ViralAgent)
(documentation HepatitisVirus "&%Viruses that cause &%Hepatitis.") 

(=>
   (biochemicalAgentSyndrome HepatitisVirus ?DISEASE)
   (instance ?DISEASE Hepatitis))

(subclass Hepatitis ViralDisease)
(documentation Hepatitis "A class of &%ViralDiseases that attack the liver.")

(subclass HepatitisAVirus ViralAgent)
(biochemicalAgentSyndrome HepatitisAVirus HepatitisA)
(biochemicalAgentDelivery HepatitisAVirus Ingesting)
(biochemicalAgentDelivery HepatitisAVirus Poking)
(documentation HepatitisAVirus "The &%Virus that causes &%HepatitisA.")

(instance HepatitisA ViralDisease)
(instance HepatitisA VaccinatableDisease)
(diseaseSymptom HepatitisA Fever)
(documentation HepatitisA "A disease that attacks the liver and causes jaundice as 
well as flu-like symptoms.  There is no chronic infection with this disease, and 
anyone who has recovered from it will have life-time immunity to the disease.")

(subclass HepatitisBVirus ViralAgent)
(biochemicalAgentSyndrome HepatitisBVirus HepatitisB)
(biochemicalAgentDelivery HepatitisBVirus Poking)
(documentation HepatitisBVirus "The &%Virus that causes &%HepatitisB.")

(instance HepatitisB ViralDisease)
(instance HepatitisB VaccinatableDisease)
(instance HepatitisB LifeThreateningDisease)
(documentation HepatitisB "Like other forms of hepatitis, &%HepatitisB attacks the 
liver of the victim.  The disease may result in death from chronic liver disease.")

(subclass HepatitisCVirus ViralAgent)
(biochemicalAgentSyndrome HepatitisCVirus HepatitisC)
(biochemicalAgentDelivery HepatitisCVirus Poking)
(documentation HepatitisCVirus "The &%Virus that causes &%HepatitisC.")

(instance HepatitisC ViralDisease)
(instance HepatitisC LifeThreateningDisease)
(documentation HepatitisC "Like other forms of hepatitis, &%HepatitisC attacks the 
liver of the victim.  The disease often results in chronic infection and chronic 
liver disease, although the mortality rate of this disease is lower than that of 
&%HepatitisB.  Most cases of &%HepatitisC are due to injections of illegal drugs.")

(subclass Rotavirus ViralAgent)
(biochemicalAgentSyndrome Rotavirus RotavirusGastroenteritis) 
(biochemicalAgentDelivery Rotavirus Ingesting)
(documentation Rotavirus "There are three types of &%Rotavirus:  Group A, Group B, 
and Group C.  They cause an acute form of gastroenteritis, known as &%RotavirusGastroenteritis.")

(instance RotavirusGastroenteritis ViralDisease)
(diseaseSymptom RotavirusGastroenteritis Fever)
(diseaseIncubation RotavirusGastroenteritis (MeasureFn 1 DayDuration) (MeasureFn 3 DayDuration))
(documentation RotavirusGastroenteritis "A disease caused by instances of &%Rotavirus, 
and characterized by vomiting, diarrhea, and fever.") 

(subclass Aflatoxin Mycotoxin)
(biochemicalAgentSyndrome Aflatoxin Aflatoxicosis)
(documentation Aflatoxin "&%Toxins produced by fungi of the strains Aspergillus 
Flavus and Aspergillus Parasiticus.  There are four varieties of &%Aflatoxin, 
viz. B1, B2, G1, and G2.  B1 is most common and most toxic.")

(instance Aflatoxicosis LifeThreateningDisease)
(documentation Aflatoxicosis "The disease has rarely been observed in humans, but it 
can cause severe damage to the liver, including cirrhosis and liver cancer, in a large 
number of animal species.")

(subclass Diacetoxyscirpenol Mycotoxin)
(lethalDose Diacetoxyscirpenol (PerFn (MeasureFn 23 (MilliFn Gram)) (MeasureFn 1 (KiloFn Gram)))) 
(documentation Diacetoxyscirpenol "A &%Micotoxin that is found in grains and products 
made from grains.  Symptoms include diarrhea, necrotic lesions, and hemorrhaging.")

(subclass T2Toxin Mycotoxin)
(lethalDose T2Toxin (PerFn (MeasureFn 5.2 (MilliFn Gram)) (MeasureFn 1 (KiloFn Gram))))
(documentation T2Toxin "A &%Micotoxin that is found in grains and products 
made from grains.  Symptoms include diarrhea, necrotic lesions, and hemorrhaging.")

(subclass BacillusCereus BacterialAgent)
(biochemicalAgentDelivery BacillusCereus Ingesting)
(documentation BacillusCereus "A &%Bacterium that is closely related to and often 
found near &%BacillusAnthracis, the &%Bacterium that causes &%Anthrax.  &%BacillusCereus 
causes two forms of food poisoning, one characterized by diarrhea and the other by 
vomiting.  In most cases the disease is not life-threatening.")   

(subclass LassaVirus ViralAgent)
(biochemicalAgentSyndrome LassaVirus LassaFever)
(biochemicalAgentDelivery LassaVirus Ingesting)
(biochemicalAgentDelivery LassaVirus Touching)
(biologicalAgentCarrier LassaVirus Rodent)
(documentation LassaVirus "The &%Virus that causes &%LassaFever.")

(instance LassaFever ViralDisease)
(instance LassaFever LifeThreateningDisease)
(diseaseSymptom LassaFever Fever)
(diseaseIncubation LassaFever (MeasureFn 1 WeekDuration) (MeasureFn 3 WeekDuration))
(diseaseMortality LassaFever 0.01)
(documentation LassaFever "A viral, rodent-carried disease found in West Africa.  The 
symptoms of the disease are wide-ranging and can be life-threatening.  The disease can 
lead to permanent neurological problems, including deafness.")

(subclass PyridostigmineBromide BiologicallyActiveSubstance)
(documentation PyridostigmineBromide "A substance that has been shown to protect lower 
primates from the lethal effects of &%Soman.  Although there is no proof that it does 
the same for humans, it was administered to soldiers in the Gulf War.") 

(subclass Legionella BacterialAgent)
(biochemicalAgentSyndrome Legionella Legionellosis)
(documentation Legionella "The &%Bacterium that causes &%Legionellosis, which is known 
as &%LegionnairesDisease (when it infects the lungs) and &%PontiacFever (when it doesn't 
affect the lungs).")

(instance Legionellosis BacterialDisease)
(documentation Legionellosis "The disease caused by the &%Bacterium &%Legionella.")

(subAttribute LegionnairesDisease Legionellosis)
(instance LegionnairesDisease LifeThreateningDisease)
(diseaseMortality LegionnairesDisease 0.125)
(documentation LegionnairesDisease "The most severe form of &%Legionellosis.  It is 
characterized by pneumonia.")

(subAttribute PontiacFever Legionellosis)
(documentation PontiacFever "A flu-like illness which is caused by &%Legionella, but 
which does not involve pneumonia.")

(subclass CorynebacteriumDiphtheriae BacterialAgent)
(biochemicalAgentSyndrome CorynebacteriumDiphtheriae Diphtheria)
(documentation CorynebacteriumDiphtheriae "The &%Bacterium that secretes a &%Toxin 
that causes &%Diphtheria.")

(instance Diphtheria BacterialDisease)
(instance Diphtheria LifeThreateningDisease)
(diseaseIncubation Diphtheria (MeasureFn 2 DayDuration) (MeasureFn 5 DayDuration))
(diseaseSymptom Diphtheria Fever)
(documentation Diphtheria "The disease can infect almost any mucous membrane.  It causes 
fever, chills, headache, and nausea.  In severe cases it may affect heart rhythm and motor 
coordination.") 

(instance CrimeanCongoHemorrhagicFever HemorrhagicFever)
(instance CrimeanCongoHemorrhagicFever LifeThreateningDisease)
(diseaseIncubation CrimeanCongoHemorrhagicFever (MeasureFn 5 DayDuration) (MeasureFn 13 DayDuration))
(diseaseSymptom CrimeanCongoHemorrhagicFever Fever) 
(diseaseMortality CrimeanCongoHemorrhagicFever 0.3) 
(documentation CrimeanCongoHemorrhagicFever "A widespread disease which was initially identified 
in the Crimea and the Congo, which accounts for its name.  The disease has a high mortality rate 
for humans, but infection of humans occurs infrequently.")  

(subclass CrimeanCongoHemorrhagicFeverVirus ViralAgent)
(biologicalAgentCarrier CrimeanCongoHemorrhagicFeverVirus Arachnid) 
(biochemicalAgentSyndrome CrimeanCongoHemorrhagicFeverVirus CrimeanCongoHemorrhagicFever) 
(biochemicalAgentDelivery CrimeanCongoHemorrhagicFeverVirus Poking)
(documentation CrimeanCongoHemorrhagicFeverVirus "The &%Virus that causes &%CrimeanCongoHemorrhagicFever.") 

(subclass JuninVirus ViralAgent)
(biologicalAgentCarrier JuninVirus Rodent)
(biochemicalAgentSyndrome JuninVirus ArgentinianHemorrhagicFever)
(biochemicalAgentDelivery JuninVirus Breathing)
(documentation JuninVirus "This &%Virus derives its name from the Junin area of Argentina, 
where occurrences have so far been restricted.")

(instance ArgentinianHemorrhagicFever HemorrhagicFever)
(diseaseSymptom ArgentinianHemorrhagicFever Fever)
(documentation ArgentinianHemorrhagicFever "Symptoms of the disease are flu-like in the initial 
stages.  As the disease progresses, symptoms include bleeding from the nose and gums and blood 
spots.")   

(subclass MachupoVirus ViralAgent)
(biologicalAgentCarrier MachupoVirus Rodent)
(biochemicalAgentSyndrome MachupoVirus BolivianHemorrhagicFever)
(biochemicalAgentDelivery MachupoVirus Breathing)
(documentation MachupoVirus "The &%Virus that causes &%BolivianHemorrhagicFever.")

(instance BolivianHemorrhagicFever HemorrhagicFever)
(diseaseSymptom BolivianHemorrhagicFever Fever)
(documentation BolivianHemorrhagicFever "Symptoms of the disease are flu-like in the initial 
stages.  As the disease progresses, symptoms include bleeding from the nose and gums and blood 
spots.")  

(subclass GuanaritoVirus ViralAgent)
(biochemicalAgentSyndrome GuanaritoVirus VenezuelanHemorrhagicFever)
(documentation GuanaritoVirus "The &%Virus that causes &%VenezuelanHemorrhagicFever.")

(instance VenezuelanHemorrhagicFever HemorrhagicFever)
(diseaseSymptom VenezuelanHemorrhagicFever Fever)
(documentation VenezuelanHemorrhagicFever "Symptoms of the disease are flu-like in the initial 
stages.  As the disease progresses, symptoms include bleeding from the nose and gums and blood 
spots.")   

(subclass SabiaVirus ViralAgent)
(biochemicalAgentSyndrome SabiaVirus BrazilianHemorrhagicFever)
(documentation SabiaVirus "The &%Virus that causes &%BrazilianHemorrhagicFever.")

(instance BrazilianHemorrhagicFever HemorrhagicFever)
(diseaseSymptom BrazilianHemorrhagicFever Fever)
(documentation BrazilianHemorrhagicFever "Symptoms of the disease are flu-like in the initial 
stages.  As the disease progresses, symptoms include bleeding from the nose and gums and blood 
spots.") 

(subclass FlexalVirus ViralAgent)
(documentation FlexalVirus "A &%Virus that that is responsible for a hemorrhagic fever found in 
South America.")  

(subclass TickBorneEncephalitisVirus ViralAgent)
(subclass TickBorneEncephalitisVirus LifeThreateningAgent)
(biologicalAgentCarrier TickBorneEncephalitisVirus Arachnid)
(biochemicalAgentDelivery TickBorneEncephalitisVirus Poking)
(documentation TickBorneEncephalitisVirus "Any &%Virus whose host is a tick and which 
carries an instance of &%TickBorneEncephalitis.")  

(=>
   (subclass ?VIRUS TickBorneEncephalitisVirus)
   (exists (?DISEASE)
      (and
         (instance ?DISEASE TickBorneEncephalitis)
         (biochemicalAgentSyndrome ?VIRUS ?DISEASE))))

(subclass TickBorneEncephalitis ViralDisease)
(subclass TickBorneEncephalitis VaccinatableDisease)
(documentation TickBorneEncephalitis "Any of various forms of encephalitis that are 
carried by ticks.  The diseases have three phases.  The first consists of flu-like 
symptoms, the second is asymptomatic, and the third involves the central nervous 
system.") 

(subclass KyasanurForestDiseaseVirus TickBorneEncephalitisVirus)
(documentation KyasanurForestDiseaseVirus "The &%Virus that carries &%KyasanurForestDisease.")

(instance KyasanurForestDisease TickBorneEncephalitis)
(diseaseIncubation KyasanurForestDisease (MeasureFn 3 DayDuration) (MeasureFn 8 DayDuration))
(documentation KyasanurForestDisease "A tick-borne encephalitis that is largely restricted 
to the Shimoga and Kanara district of Karnataka, India.")

(subclass HendraVirus ViralAgent)
(documentation HendraVirus "A &%Virus found in Australia and Papua New Guinea that 
infects humans and horses.  The natural host of the virus appears to be fruit bats.  
Not much is known about the disease caused by the &%HendraVirus, but it seems to begin 
with fever and then progress to drowsiness and coma.")

(subclass NipahVirus ViralAgent)
(documentation NipahVirus "Closely related to &%HendraVirus.  Like the &%HendraVirus, 
the natural host of &%NipahVirus appears to be fruit bats.  Not much is known about 
the disease, but its symptoms include high fever, muscle pain, and eventually 
encephalitis, convulsions, and coma.")











































